-- =====================================================================
-- Author:			Inflectra Corporation
-- Business Object: Incident
-- Description:		Retrieves the list of incidents for a given test case
-- =====================================================================
IF OBJECT_ID ( 'INCIDENT_RETRIEVE_BY_TEST_CASE', 'P' ) IS NOT NULL 
    DROP PROCEDURE INCIDENT_RETRIEVE_BY_TEST_CASE;
GO
CREATE PROCEDURE INCIDENT_RETRIEVE_BY_TEST_CASE
	@TestCaseId INT,
	@IsOpenOnly BIT
AS
BEGIN
	SELECT	INC.*
	FROM	VW_INCIDENT_LIST INC INNER JOIN TST_TEST_RUN_STEP_INCIDENT TRI
	ON		INC.INCIDENT_ID = TRI.INCIDENT_ID INNER JOIN TST_TEST_RUN_STEP TRS
	ON		TRI.TEST_RUN_STEP_ID = TRS.TEST_RUN_STEP_ID INNER JOIN TST_TEST_RUN TRN
	ON		TRS.TEST_RUN_ID = TRN.TEST_RUN_ID
	WHERE	TRN.TEST_CASE_ID = @TestCaseId
	AND		INC.IS_DELETED = 0
	AND		(@IsOpenOnly = 0 OR INC.INCIDENT_STATUS_IS_OPEN_STATUS = 1)
	UNION
    SELECT DISTINCT  INC.*
    FROM	VW_INCIDENT_LIST INC INNER JOIN TST_ARTIFACT_LINK ARL
    ON		INC.INCIDENT_ID = ARL.DEST_ARTIFACT_ID INNER JOIN TST_TEST_STEP STP
    ON		ARL.SOURCE_ARTIFACT_ID = STP.TEST_STEP_ID
    WHERE	STP.TEST_CASE_ID = @TestCaseId
    AND    INC.IS_DELETED = 0
    AND    ARL.SOURCE_ARTIFACT_TYPE_ID = 7 /* Test Step */
    AND    ARL.DEST_ARTIFACT_TYPE_ID = 3 /* Incident */ 
    AND		(@IsOpenOnly = 0 OR INC.INCIDENT_STATUS_IS_OPEN_STATUS = 1)
	ORDER BY INC.PRIORITY_NAME, INC.INCIDENT_ID
END
GO
