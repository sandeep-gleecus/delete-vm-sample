<?xml version="1.0" encoding="utf-8"?>
<xsl:stylesheet version="1.0" xmlns:xsl="http://www.w3.org/1999/XSL/Transform" xmlns:msxsl="urn:schemas-microsoft-com:xslt" exclude-result-prefixes="msxsl">
  <xsl:template match="/ReleaseData">
    <table class="DataGrid" style="width:100%">
      <tr>
        <th colspan="10" style="text-align:center">
          General
        </th>
        <th colspan="5" style="text-align:center">
          Testing Information
        </th>
        <th colspan="7" style="text-align:center">
          Development Information
        </th>
        <th>
          <xsl:attribute name="colspan">
            <xsl:value-of select="count(Release[1]/CustomProperties/CustomProperty)"/>
          </xsl:attribute>
          Custom Properties
        </th>
      </tr>
      <tr>
        <th>Rel #</th>
        <th>Name</th>
        <th>Version Number</th>
        <th>Description</th>
        <th>Creator</th>
        <th>Creation Date</th>
        <th>Type</th>
        <th>Status</th>
        <th>Start Date</th>
        <th>End Date</th>
        <th># Failed</th>
        <th># Passed</th>
        <th># Blocked</th>
        <th># Caution</th>
        <th># Not Run</th>
        <th>Task Progress</th>
        <th>Planned Effort</th>
        <th>Available Effort</th>
        <th>Est. Effort</th>
        <th>Actual Effort</th>
        <th>Remaining Effort</th>
        <th>Projected Effort</th>
        <th># Resources</th>
        <th>Non-Working Days</th>
        <xsl:for-each select="Release[1]/CustomProperties/CustomProperty">
          <th>
            <xsl:value-of select="Alias"/>
          </th>
        </xsl:for-each>
      </tr>
      <xsl:for-each select="Release">
        <tr>
          <td>
            <xsl:value-of select="ReleaseId"/>
          </td>
          <td>
            <xsl:attribute name="style">
              padding-left: <xsl:value-of select="string-length(IndentLevel)*2"/>px;
            </xsl:attribute>
            <xsl:if test="IsIterationOrPhase='False'">
              <b>
                <xsl:value-of select="Name"/>
              </b>
            </xsl:if>
            <xsl:if test="IsIterationOrPhase='True'">
              <xsl:value-of select="Name"/>
            </xsl:if>
          </td>
          <td>
            <xsl:value-of select="VersionNumber"/>
          </td>
          <td>
            <xsl:value-of select="Description" disable-output-escaping="yes"/>
          </td>
          <td>
            <xsl:value-of select="CreatorName"/>
          </td>
          <td class="Date">
            <xsl:call-template name="format-date">
              <xsl:with-param name="datetime" select="CreationDate" />
            </xsl:call-template>
          </td>
          <td>
            <xsl:value-of select="ReleaseTypeName"/>
          </td>
          <td>
            <xsl:value-of select="ReleaseStatusName"/>
          </td>
          <td class="Date">
            <xsl:call-template name="format-date">
              <xsl:with-param name="datetime" select="StartDate" />
            </xsl:call-template>
          </td>
          <td class="Date">
            <xsl:call-template name="format-date">
              <xsl:with-param name="datetime" select="EndDate" />
            </xsl:call-template>
          </td>
          <td>
            <xsl:value-of select="CountFailed"/>
          </td>
          <td>
            <xsl:value-of select="CountPassed"/>
          </td>
          <td>
            <xsl:value-of select="CountBlocked"/>
          </td>
          <td>
            <xsl:value-of select="CountCaution"/>
          </td>
          <td>
            <xsl:value-of select="CountNotRun"/>
          </td>
          <td>
            <xsl:value-of select="TaskCount"/><xsl:text> Tasks</xsl:text>
            <xsl:value-of select="TaskPercentOnTime"/><xsl:text>% On Schedule,</xsl:text>
            <xsl:value-of select="TaskPercentLateFinish"/><xsl:text>% Running Late,</xsl:text>
            <xsl:value-of select="TaskPercentNotStart"/><xsl:text>% Starting Late,</xsl:text>
            <xsl:value-of select="TaskPercentLateStart"/><xsl:text>% Not Started</xsl:text>
          </td>
          <td class="Timespan">
            <xsl:value-of select="PlannedEffort"/>
          </td>
          <td class="Timespan">
            <xsl:value-of select="AvailableEffort"/>
          </td>
          <td class="Timespan">
            <xsl:value-of select="TaskEstimatedEffort"/>
          </td>
          <td class="Timespan">
            <xsl:value-of select="TaskActualEffort"/>
          </td>
          <td class="Timespan">
            <xsl:value-of select="TaskRemainingEffort"/>
          </td>
          <td class="Timespan">
            <xsl:value-of select="TaskProjectedEffort"/>
          </td>
          <td>
            <xsl:value-of select="ResourceCount"/>
          </td>
          <td>
            <xsl:value-of select="DaysNonWorking"/>
          </td>

          <xsl:for-each select="CustomProperties/CustomProperty">
            <xsl:choose>
              <xsl:when test="Type='Date'">
                <td class="Date">
                  <xsl:call-template name="format-date">
                    <xsl:with-param name="datetime" select="Value" />
                  </xsl:call-template>
                </td>
              </xsl:when>
              <xsl:otherwise>
                <td>
                  <xsl:value-of select="Value" disable-output-escaping="yes"/>
                </td>
              </xsl:otherwise>
            </xsl:choose>
          </xsl:for-each>
        </tr>
      </xsl:for-each>
    </table>
  </xsl:template>
  <xsl:template name="format-date">
    <xsl:param name="datetime"/>
    <xsl:variable name="date" select="substring-before($datetime, 'T')" />
    <xsl:variable name="year" select="substring-before($date, '-')" />
    <xsl:variable name="month" select="substring-before(substring-after($date, '-'), '-')" />
    <xsl:variable name="day" select="substring-after(substring-after($date, '-'), '-')" />
    <xsl:variable name="time" select="substring-before(substring-after($datetime, 'T'), '.')" />
    <xsl:variable name="monthname">
      <xsl:choose>
        <xsl:when test="$month='01'">
          <xsl:value-of select="'Jan'"/>
        </xsl:when>
        <xsl:when test="$month='02'">
          <xsl:value-of select="'Feb'"/>
        </xsl:when>
        <xsl:when test="$month='03'">
          <xsl:value-of select="'Mar'"/>
        </xsl:when>
        <xsl:when test="$month='04'">
          <xsl:value-of select="'Apr'"/>
        </xsl:when>
        <xsl:when test="$month='05'">
          <xsl:value-of select="'May'"/>
        </xsl:when>
        <xsl:when test="$month='06'">
          <xsl:value-of select="'Jun'"/>
        </xsl:when>
        <xsl:when test="$month='07'">
          <xsl:value-of select="'Jul'"/>
        </xsl:when>
        <xsl:when test="$month='08'">
          <xsl:value-of select="'Aug'"/>
        </xsl:when>
        <xsl:when test="$month='09'">
          <xsl:value-of select="'Sep'"/>
        </xsl:when>
        <xsl:when test="$month='10'">
          <xsl:value-of select="'Oct'"/>
        </xsl:when>
        <xsl:when test="$month='11'">
          <xsl:value-of select="'Nov'"/>
        </xsl:when>
        <xsl:when test="$month='12'">
          <xsl:value-of select="'Dec'"/>
        </xsl:when>
        <xsl:otherwise>
          <xsl:value-of select="''" />
        </xsl:otherwise>
      </xsl:choose>
    </xsl:variable>
    <xsl:value-of select="concat($day, '-' ,$monthname, '-', $year , ' ', $time)" />
  </xsl:template>
</xsl:stylesheet>

