-- =============================================
-- Author:			Inflectra Corporation
-- Business Object: Task
-- Description:		Retrieves the project group summary status for tasks by project
-- =============================================
IF OBJECT_ID ( 'TASK_RETRIEVE_GROUP_SUMMARY_BY_PROJECT', 'P' ) IS NOT NULL 
    DROP PROCEDURE TASK_RETRIEVE_GROUP_SUMMARY_BY_PROJECT;
GO
CREATE PROCEDURE TASK_RETRIEVE_GROUP_SUMMARY_BY_PROJECT
	@ProjectGroupId INT,
	@ActiveReleasesOnly BIT
AS
BEGIN
	SELECT
		PRJ2.PROJECT_GROUP_ID,
		PRJ2.PROJECT_ID,
		PRJ2.NAME AS PROJECT_NAME,
		PRJ2.DESCRIPTION AS PROJECT_DESCRIPTION,
		ISNULL(PTP.TASK_COUNT,0) AS TASK_COUNT,
		PTP.TASK_PERCENT_ON_TIME,
		PTP.TASK_PERCENT_LATE_FINISH,
		PTP.TASK_PERCENT_NOT_START,
		PTP.TASK_PERCENT_LATE_START,
		PTP.TASK_ESTIMATED_EFFORT,
		PTP.TASK_ACTUAL_EFFORT,
		PTP.TASK_REMAINING_EFFORT,
		PTP.TASK_PROJECTED_EFFORT

	FROM
		(SELECT
			PRJ.PROJECT_ID,
			SUM(REL.TASK_COUNT) AS TASK_COUNT,
			ISNULL(AVG(REL.TASK_PERCENT_ON_TIME),0) AS TASK_PERCENT_ON_TIME,
			ISNULL(AVG(REL.TASK_PERCENT_LATE_FINISH),0) AS TASK_PERCENT_LATE_FINISH,
			ISNULL(AVG(REL.TASK_PERCENT_NOT_START),0) AS TASK_PERCENT_NOT_START,
			ISNULL(AVG(REL.TASK_PERCENT_LATE_START),0) AS TASK_PERCENT_LATE_START,
			ISNULL(SUM(REL.TASK_ESTIMATED_EFFORT),0) AS TASK_ESTIMATED_EFFORT,
			ISNULL(SUM(REL.TASK_ACTUAL_EFFORT),0) AS TASK_ACTUAL_EFFORT,
			ISNULL(SUM(REL.TASK_REMAINING_EFFORT),0) AS TASK_REMAINING_EFFORT,
			ISNULL(SUM(REL.TASK_PROJECTED_EFFORT),0) AS TASK_PROJECTED_EFFORT
		FROM TST_RELEASE REL
			INNER JOIN TST_PROJECT PRJ ON REL.PROJECT_ID = PRJ.PROJECT_ID
		WHERE (REL.RELEASE_TYPE_ID = 1 OR LEN(REL.INDENT_LEVEL) = 3)
			AND (REL.TASK_COUNT > 0)
			AND REL.IS_DELETED = 0
			AND PRJ.IS_ACTIVE = 1
			AND (REL.RELEASE_STATUS_ID NOT IN (4 /*Closed*/, 5 /*Deferred*/, 6 /*Cancelled*/) OR @ActiveReleasesOnly = 0)
		GROUP BY PRJ.PROJECT_ID
		) AS PTP
			RIGHT JOIN TST_PROJECT PRJ2 ON PRJ2.PROJECT_ID = PTP.PROJECT_ID
			WHERE PRJ2.IS_ACTIVE = 1 AND PRJ2.PROJECT_GROUP_ID = @ProjectGroupId
END
GO
