-- =============================================
-- Author:			Inflectra Corporation
-- Business Object: Template
-- Description:		Changes the requirement fields in a project from one template to another
-- Remarks:			It maps field by name where possible, otherwise just uses the defaults
-- =============================================
IF OBJECT_ID ( 'TEMPLATE_REMAP_REQUIREMENT_FIELDS', 'P' ) IS NOT NULL 
    DROP PROCEDURE TEMPLATE_REMAP_REQUIREMENT_FIELDS;
GO
CREATE PROCEDURE TEMPLATE_REMAP_REQUIREMENT_FIELDS
	@ProjectId INT,
	@OldTemplateId INT,
	@NewTemplateId INT
AS
BEGIN
	SET NOCOUNT ON;

-- Requirement Types	
UPDATE INC
	SET INC.REQUIREMENT_TYPE_ID = MAP.NEW_TYPE_ID
FROM 
	TST_REQUIREMENT INC INNER JOIN (
	SELECT INC.REQUIREMENT_ID, T1.REQUIREMENT_TYPE_ID AS OLD_TYPE_ID, T2.REQUIREMENT_TYPE_ID AS NEW_TYPE_ID
	FROM TST_REQUIREMENT INC
		INNER JOIN TST_REQUIREMENT_TYPE T1 ON INC.REQUIREMENT_TYPE_ID = T1.REQUIREMENT_TYPE_ID
		INNER JOIN TST_REQUIREMENT_TYPE T2 ON T1.NAME = T2.NAME		
	WHERE INC.PROJECT_ID = @ProjectId
		AND T1.PROJECT_TEMPLATE_ID = @OldTemplateId
		AND T2.PROJECT_TEMPLATE_ID = @NewTemplateId
		AND INC.REQUIREMENT_TYPE_ID > 0 /* Exclude Package */
	UNION	
	SELECT INC.REQUIREMENT_ID, T1.REQUIREMENT_TYPE_ID AS OLD_TYPE_ID, (SELECT TOP 1 REQUIREMENT_TYPE_ID FROM TST_REQUIREMENT_TYPE WHERE IS_DEFAULT = 1 AND PROJECT_TEMPLATE_ID = @NewTemplateId) AS NEW_TYPE_ID
	FROM TST_REQUIREMENT INC
		INNER JOIN TST_REQUIREMENT_TYPE T1 ON INC.REQUIREMENT_TYPE_ID = T1.REQUIREMENT_TYPE_ID
	WHERE INC.PROJECT_ID = @ProjectId
		AND T1.PROJECT_TEMPLATE_ID = @OldTemplateId
		AND T1.NAME NOT IN (SELECT NAME FROM TST_REQUIREMENT_TYPE WHERE PROJECT_TEMPLATE_ID = @NewTemplateId)
	) MAP
	ON INC.REQUIREMENT_ID = MAP.REQUIREMENT_ID
WHERE 
	MAP.REQUIREMENT_ID = INC.REQUIREMENT_ID

--Requirement Importance
UPDATE INC
	SET INC.IMPORTANCE_ID = MAP.NEW_IMPORTANCE_ID
FROM 
	TST_REQUIREMENT INC INNER JOIN (
	SELECT INC.REQUIREMENT_ID, T1.IMPORTANCE_ID AS OLD_IMPORTANCE_ID, T2.IMPORTANCE_ID AS NEW_IMPORTANCE_ID
	FROM TST_REQUIREMENT INC
		INNER JOIN TST_IMPORTANCE T1 ON INC.IMPORTANCE_ID = T1.IMPORTANCE_ID
		INNER JOIN TST_IMPORTANCE T2 ON T1.NAME = T2.NAME		
	WHERE INC.PROJECT_ID = @ProjectId
		AND T1.PROJECT_TEMPLATE_ID = @OldTemplateId
		AND T2.PROJECT_TEMPLATE_ID = @NewTemplateId
	UNION	
	SELECT INC.REQUIREMENT_ID, T1.IMPORTANCE_ID AS OLD_IMPORTANCE_ID, NULL AS NEW_IMPORTANCE_ID
	FROM TST_REQUIREMENT INC
		INNER JOIN TST_IMPORTANCE T1 ON INC.IMPORTANCE_ID = T1.IMPORTANCE_ID
	WHERE INC.PROJECT_ID = @ProjectId
		AND T1.PROJECT_TEMPLATE_ID = @OldTemplateId
		AND T1.NAME NOT IN (SELECT NAME FROM TST_IMPORTANCE WHERE PROJECT_TEMPLATE_ID = @NewTemplateId)
	) MAP		
	ON INC.REQUIREMENT_ID = MAP.REQUIREMENT_ID
WHERE 
	MAP.REQUIREMENT_ID = INC.REQUIREMENT_ID
		
END
GO
