-- =====================================================================
-- Author:			Inflectra Corporation
-- Business Object: DataSyncManager
-- Description:		Retrieves a dataset containing the list of field values and their mappings
-- =====================================================================
IF OBJECT_ID ( 'DATA_SYNC_RETRIEVE_FIELD_VALUE_MAPPINGS', 'P' ) IS NOT NULL 
    DROP PROCEDURE [DATA_SYNC_RETRIEVE_FIELD_VALUE_MAPPINGS];
GO
CREATE PROCEDURE [DATA_SYNC_RETRIEVE_FIELD_VALUE_MAPPINGS]
	@DataSyncSystemId INT,
	@ProjectId INT,
	@ArtifactFieldId INT,
	@IncludeUnmapped BIT
AS
BEGIN
	DECLARE @tblLookupData TABLE
	(
	  ID INT, 
	  NAME NVARCHAR(255),
	  IS_ACTIVE BIT
	)
	
	/* The lookup to use depends on the artifact field */
	IF @ArtifactFieldId = 1
	BEGIN
		--Incident Severity
		INSERT INTO @tblLookupData (ID, NAME, IS_ACTIVE)
		SELECT INC.SEVERITY_ID, INC.NAME, INC.IS_ACTIVE
		FROM TST_INCIDENT_SEVERITY INC
			INNER JOIN TST_PROJECT PRJ ON INC.PROJECT_TEMPLATE_ID = PRJ.PROJECT_TEMPLATE_ID
		WHERE PRJ.PROJECT_ID = @ProjectId 
	END
	IF @ArtifactFieldId = 2
	BEGIN
		--Incident Priority
		INSERT INTO @tblLookupData (ID, NAME, IS_ACTIVE)
		SELECT INC.PRIORITY_ID, INC.NAME, INC.IS_ACTIVE
		FROM TST_INCIDENT_PRIORITY INC
			INNER JOIN TST_PROJECT PRJ ON INC.PROJECT_TEMPLATE_ID = PRJ.PROJECT_TEMPLATE_ID
		WHERE PRJ.PROJECT_ID = @ProjectId 
	END
	IF @ArtifactFieldId = 3
	BEGIN
		--Incident Status
		INSERT INTO @tblLookupData (ID, NAME, IS_ACTIVE)
		SELECT INC.INCIDENT_STATUS_ID, INC.NAME, INC.IS_ACTIVE
		FROM TST_INCIDENT_STATUS INC
			INNER JOIN TST_PROJECT PRJ ON INC.PROJECT_TEMPLATE_ID = PRJ.PROJECT_TEMPLATE_ID
		WHERE PRJ.PROJECT_ID = @ProjectId 
	END
	IF @ArtifactFieldId = 4
	BEGIN
		--Incident Type
		INSERT INTO @tblLookupData (ID, NAME, IS_ACTIVE)
		SELECT INC.INCIDENT_TYPE_ID, INC.NAME, INC.IS_ACTIVE
		FROM TST_INCIDENT_TYPE INC
			INNER JOIN TST_PROJECT PRJ ON INC.PROJECT_TEMPLATE_ID = PRJ.PROJECT_TEMPLATE_ID
		WHERE PRJ.PROJECT_ID = @ProjectId 
	END
	IF @ArtifactFieldId = 148 OR @ArtifactFieldId = 141 OR @ArtifactFieldId = 168
	BEGIN
		--Incident/Requirement/Test Case Component
		INSERT INTO @tblLookupData (ID, NAME, IS_ACTIVE)
		SELECT COMPONENT_ID, NAME, IS_ACTIVE
		FROM TST_COMPONENT
		WHERE PROJECT_ID = @ProjectId 
	END
	IF @ArtifactFieldId = 57
	BEGIN
		--Task Status
		INSERT INTO @tblLookupData (ID, NAME, IS_ACTIVE)
		SELECT TASK_STATUS_ID, NAME, IS_ACTIVE FROM TST_TASK_STATUS
	END
	IF @ArtifactFieldId = 59
	BEGIN
		--Task Priority
		INSERT INTO @tblLookupData (ID, NAME, IS_ACTIVE)
		SELECT TSK.TASK_PRIORITY_ID, TSK.NAME, TSK.IS_ACTIVE FROM TST_TASK_PRIORITY TSK
			INNER JOIN TST_PROJECT PRJ ON TSK.PROJECT_TEMPLATE_ID = PRJ.PROJECT_TEMPLATE_ID
		WHERE PRJ.PROJECT_ID = @ProjectId 
	END
	IF @ArtifactFieldId = 145
	BEGIN
		--Task Type
		INSERT INTO @tblLookupData (ID, NAME, IS_ACTIVE)
		SELECT TSK.TASK_TYPE_ID, TSK.NAME, TSK.IS_ACTIVE FROM TST_TASK_TYPE TSK
			INNER JOIN TST_PROJECT PRJ ON TSK.PROJECT_TEMPLATE_ID = PRJ.PROJECT_TEMPLATE_ID
		WHERE PRJ.PROJECT_ID = @ProjectId 
	END
	IF @ArtifactFieldId = 18
	BEGIN
		--Requirement Importance
		INSERT INTO @tblLookupData (ID, NAME, IS_ACTIVE)
		SELECT REQ.IMPORTANCE_ID, REQ.NAME, REQ.IS_ACTIVE FROM TST_IMPORTANCE REQ
			INNER JOIN TST_PROJECT PRJ ON REQ.PROJECT_TEMPLATE_ID = PRJ.PROJECT_TEMPLATE_ID
		WHERE PRJ.PROJECT_ID = @ProjectId 
	END
	IF @ArtifactFieldId = 16
	BEGIN
		--Requirement Status
		INSERT INTO @tblLookupData (ID, NAME, IS_ACTIVE)
		SELECT REQUIREMENT_STATUS_ID, NAME, IS_ACTIVE FROM TST_REQUIREMENT_STATUS
	END
	IF @ArtifactFieldId = 140
	BEGIN
		--Requirement Type
		INSERT INTO @tblLookupData (ID, NAME, IS_ACTIVE)
		SELECT REQ.REQUIREMENT_TYPE_ID, REQ.NAME, REQ.IS_ACTIVE FROM TST_REQUIREMENT_TYPE REQ
			INNER JOIN TST_PROJECT PRJ ON REQ.PROJECT_TEMPLATE_ID = PRJ.PROJECT_TEMPLATE_ID
		WHERE PRJ.PROJECT_ID = @ProjectId 
	END
	IF @ArtifactFieldId = 24
	BEGIN
		--Test Case Priority
		INSERT INTO @tblLookupData (ID, NAME, IS_ACTIVE)
		SELECT TST.TEST_CASE_PRIORITY_ID, TST.NAME, TST.IS_ACTIVE FROM TST_TEST_CASE_PRIORITY TST
			INNER JOIN TST_PROJECT PRJ ON TST.PROJECT_TEMPLATE_ID = PRJ.PROJECT_TEMPLATE_ID
		WHERE PRJ.PROJECT_ID = @ProjectId 
	END
	IF @ArtifactFieldId = 166
	BEGIN
		--Test Case Status
		INSERT INTO @tblLookupData (ID, NAME, IS_ACTIVE)
		SELECT TEST_CASE_STATUS_ID, NAME, IS_ACTIVE FROM TST_TEST_CASE_STATUS
	END
	IF @ArtifactFieldId = 167
	BEGIN
		--Test Case Type
		INSERT INTO @tblLookupData (ID, NAME, IS_ACTIVE)
		SELECT TST.TEST_CASE_TYPE_ID, TST.NAME, TST.IS_ACTIVE FROM TST_TEST_CASE_TYPE TST
			INNER JOIN TST_PROJECT PRJ ON TST.PROJECT_TEMPLATE_ID = PRJ.PROJECT_TEMPLATE_ID
		WHERE PRJ.PROJECT_ID = @ProjectId 
	END

	IF @IncludeUnmapped = 1
	BEGIN
		SELECT	@DataSyncSystemId AS DATA_SYNC_SYSTEM_ID, @ProjectId AS PROJECT_ID, @ArtifactFieldId AS ARTIFACT_FIELD_ID,
				LKP.ID AS ARTIFACT_FIELD_VALUE, FVM.EXTERNAL_KEY, ISNULL(FVM.PRIMARY_YN, 'Y') AS PRIMARY_YN,
				LKP.NAME AS ARTIFACT_FIELD_VALUE_NAME, LKP.IS_ACTIVE
		FROM
			(SELECT * FROM TST_DATA_SYNC_ARTIFACT_FIELD_VALUE_MAPPING 
			WHERE ARTIFACT_FIELD_ID = @ArtifactFieldId
			AND DATA_SYNC_SYSTEM_ID = @DataSyncSystemId
			AND PROJECT_ID = @ProjectId) FVM
		RIGHT JOIN @tblLookupData LKP ON FVM.ARTIFACT_FIELD_VALUE = LKP.ID
		ORDER BY LKP.NAME, FVM.ARTIFACT_FIELD_ID
	END
	ELSE
	BEGIN
		SELECT	FVM.DATA_SYNC_SYSTEM_ID, FVM.PROJECT_ID, FVM.ARTIFACT_FIELD_ID, FVM.ARTIFACT_FIELD_VALUE,
                FVM.EXTERNAL_KEY, FVM.PRIMARY_YN, LKP.NAME AS ARTIFACT_FIELD_VALUE_NAME, LKP.IS_ACTIVE
		FROM	TST_DATA_SYNC_ARTIFACT_FIELD_VALUE_MAPPING FVM INNER JOIN @tblLookupData LKP
		ON     FVM.ARTIFACT_FIELD_VALUE = LKP.ID
		WHERE  FVM.DATA_SYNC_SYSTEM_ID = @DataSyncSystemId
		AND    FVM.PROJECT_ID = @ProjectId
		AND    FVM.ARTIFACT_FIELD_ID = @ArtifactFieldId
		ORDER BY LKP.NAME, FVM.ARTIFACT_FIELD_ID
	END
END
GO
