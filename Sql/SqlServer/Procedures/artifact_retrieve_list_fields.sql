-- =====================================================================
-- Author:			Inflectra Corporation
-- Business Object: ArtifactManager
-- Description:		Retrieves the list of fields and their position for
--					a specific artifact, user and project
-- =====================================================================
IF OBJECT_ID ( 'ARTIFACT_RETRIEVE_LIST_FIELDS', 'P' ) IS NOT NULL 
    DROP PROCEDURE ARTIFACT_RETRIEVE_LIST_FIELDS;
GO
CREATE PROCEDURE ARTIFACT_RETRIEVE_LIST_FIELDS
	@ProjectId INT,
	@UserId INT,
	@ArtifactTypeId INT,
	@CustumPropPrefix NVARCHAR(50)
AS
BEGIN
	--Get the list of fields and custom properties
	SELECT	ARF.NAME, ARF.CAPTION, ARTIFACT_FIELD_TYPE_ID, ISNULL(UAF.IS_VISIBLE, ARF.IS_LIST_DEFAULT) AS IS_VISIBLE,
			ISNULL(UAF.LIST_POSITION, ARF.LIST_DEFAULT_POSITION) AS LIST_POSITION, ARF.LOOKUP_PROPERTY, ARF.IS_HISTORY_RECORDED,
			UAF.WIDTH
	FROM	TST_ARTIFACT_FIELD ARF LEFT JOIN
		(SELECT ARTIFACT_FIELD_ID, IS_VISIBLE, LIST_POSITION, WIDTH FROM TST_USER_ARTIFACT_FIELD WHERE PROJECT_ID = @ProjectId AND USER_ID = @UserId) UAF
	ON		ARF.ARTIFACT_FIELD_ID = UAF.ARTIFACT_FIELD_ID
	WHERE 	ARF.IS_ACTIVE = 1
	AND 	ARF.IS_LIST_CONFIG = 1
	AND	ARF.ARTIFACT_TYPE_ID = @ArtifactTypeId
	UNION
	SELECT	@CustumPropPrefix + REPLACE(STR(CPR.PROPERTY_NUMBER, 2), SPACE(1), '0') AS NAME, CPR.NAME AS CAPTION,  CPT.ARTIFACT_FIELD_TYPE_ID,
			ISNULL(UCP.IS_VISIBLE, 0) AS IS_VISIBLE, ISNULL(UCP.LIST_POSITION, (CPR.PROPERTY_NUMBER + 50)) AS LIST_POSITION, NULL AS LOOKUP_PROPERTY,
			CAST(0 AS BIT) AS IS_HISTORY_RECORDED, UCP.WIDTH
	FROM	TST_CUSTOM_PROPERTY CPR INNER JOIN TST_CUSTOM_PROPERTY_TYPE CPT
	ON		CPR.CUSTOM_PROPERTY_TYPE_ID = CPT.CUSTOM_PROPERTY_TYPE_ID LEFT JOIN
		(SELECT CUSTOM_PROPERTY_ID, IS_VISIBLE, LIST_POSITION, WIDTH FROM TST_USER_CUSTOM_PROPERTY WHERE PROJECT_ID = @ProjectId AND USER_ID = @UserId) UCP
	ON		UCP.CUSTOM_PROPERTY_ID = CPR.CUSTOM_PROPERTY_ID INNER JOIN TST_PROJECT PRJ
	ON		CPR.PROJECT_TEMPLATE_ID = PRJ.PROJECT_TEMPLATE_ID
	WHERE 	CPR.IS_DELETED = 0
	AND	CPR.ARTIFACT_TYPE_ID = @ArtifactTypeId
	AND PRJ.PROJECT_ID = @ProjectId
	ORDER BY IS_VISIBLE, LIST_POSITION
END
GO
