-- =============================================
-- Author:			Inflectra Corporation
-- Business Object: Template
-- Description:		Changes the incident fields in a project from one template to another
-- Remarks:			It maps field by name where possible, otherwise just uses the defaults
-- =============================================
IF OBJECT_ID ( 'TEMPLATE_REMAP_INCIDENT_FIELDS', 'P' ) IS NOT NULL 
    DROP PROCEDURE TEMPLATE_REMAP_INCIDENT_FIELDS;
GO
CREATE PROCEDURE TEMPLATE_REMAP_INCIDENT_FIELDS
	@ProjectId INT,
	@OldTemplateId INT,
	@NewTemplateId INT
AS
BEGIN
	SET NOCOUNT ON;

-- Incident Types	
UPDATE INC
	SET INC.INCIDENT_TYPE_ID = MAP.NEW_TYPE_ID
FROM 
	TST_INCIDENT INC INNER JOIN (
	SELECT INC.INCIDENT_ID, T1.INCIDENT_TYPE_ID AS OLD_TYPE_ID, T2.INCIDENT_TYPE_ID AS NEW_TYPE_ID
	FROM TST_INCIDENT INC
		INNER JOIN TST_INCIDENT_TYPE T1 ON INC.INCIDENT_TYPE_ID = T1.INCIDENT_TYPE_ID
		INNER JOIN TST_INCIDENT_TYPE T2 ON T1.NAME = T2.NAME		
	WHERE INC.PROJECT_ID = @ProjectId
		AND T1.PROJECT_TEMPLATE_ID = @OldTemplateId
		AND T2.PROJECT_TEMPLATE_ID = @NewTemplateId
	UNION	
	SELECT INC.INCIDENT_ID, T1.INCIDENT_TYPE_ID AS OLD_TYPE_ID, (SELECT TOP 1 INCIDENT_TYPE_ID FROM TST_INCIDENT_TYPE WHERE IS_DEFAULT = 1 AND PROJECT_TEMPLATE_ID = @NewTemplateId) AS NEW_TYPE_ID
	FROM TST_INCIDENT INC
		INNER JOIN TST_INCIDENT_TYPE T1 ON INC.INCIDENT_TYPE_ID = T1.INCIDENT_TYPE_ID
	WHERE INC.PROJECT_ID = @ProjectId
		AND T1.PROJECT_TEMPLATE_ID = @OldTemplateId
		AND T1.NAME NOT IN (SELECT NAME FROM TST_INCIDENT_TYPE WHERE PROJECT_TEMPLATE_ID = @NewTemplateId)
	) MAP
	ON INC.INCIDENT_ID = MAP.INCIDENT_ID
WHERE 
	MAP.INCIDENT_ID = INC.INCIDENT_ID

-- Incident Status
UPDATE INC
	SET INC.INCIDENT_STATUS_ID = MAP.NEW_STATUS_ID
FROM 
	TST_INCIDENT INC INNER JOIN (
	SELECT INC.INCIDENT_ID, T1.INCIDENT_STATUS_ID AS OLD_STATUS_ID, T2.INCIDENT_STATUS_ID AS NEW_STATUS_ID
	FROM TST_INCIDENT INC
		INNER JOIN TST_INCIDENT_STATUS T1 ON INC.INCIDENT_STATUS_ID = T1.INCIDENT_STATUS_ID
		INNER JOIN TST_INCIDENT_STATUS T2 ON T1.NAME = T2.NAME		
	WHERE INC.PROJECT_ID = @ProjectId
		AND T1.PROJECT_TEMPLATE_ID = @OldTemplateId
		AND T2.PROJECT_TEMPLATE_ID = @NewTemplateId
	UNION	
	SELECT INC.INCIDENT_ID, T1.INCIDENT_STATUS_ID AS OLD_STATUS_ID, (SELECT TOP 1 INCIDENT_STATUS_ID FROM TST_INCIDENT_STATUS WHERE IS_DEFAULT = 1 AND PROJECT_TEMPLATE_ID = @NewTemplateId) AS NEW_STATUS_ID
	FROM TST_INCIDENT INC
		INNER JOIN TST_INCIDENT_STATUS T1 ON INC.INCIDENT_STATUS_ID = T1.INCIDENT_STATUS_ID
	WHERE INC.PROJECT_ID = @ProjectId
		AND T1.PROJECT_TEMPLATE_ID = @OldTemplateId
		AND T1.NAME NOT IN (SELECT NAME FROM TST_INCIDENT_STATUS WHERE PROJECT_TEMPLATE_ID = @NewTemplateId)
	) MAP
	ON INC.INCIDENT_ID = MAP.INCIDENT_ID
WHERE 
	MAP.INCIDENT_ID = INC.INCIDENT_ID
	
--Incident Priority
UPDATE INC
	SET INC.PRIORITY_ID = MAP.NEW_PRIORITY_ID
FROM 
	TST_INCIDENT INC INNER JOIN (
	SELECT INC.INCIDENT_ID, T1.PRIORITY_ID AS OLD_PRIORITY_ID, T2.PRIORITY_ID AS NEW_PRIORITY_ID
	FROM TST_INCIDENT INC
		INNER JOIN TST_INCIDENT_PRIORITY T1 ON INC.PRIORITY_ID = T1.PRIORITY_ID
		INNER JOIN TST_INCIDENT_PRIORITY T2 ON T1.NAME = T2.NAME		
	WHERE INC.PROJECT_ID = @ProjectId
		AND T1.PROJECT_TEMPLATE_ID = @OldTemplateId
		AND T2.PROJECT_TEMPLATE_ID = @NewTemplateId
	UNION	
	SELECT INC.INCIDENT_ID, T1.PRIORITY_ID AS OLD_PRIORITY_ID, NULL AS NEW_PRIORITY_ID
	FROM TST_INCIDENT INC
		INNER JOIN TST_INCIDENT_PRIORITY T1 ON INC.PRIORITY_ID = T1.PRIORITY_ID
	WHERE INC.PROJECT_ID = @ProjectId
		AND T1.PROJECT_TEMPLATE_ID = @OldTemplateId
		AND T1.NAME NOT IN (SELECT NAME FROM TST_INCIDENT_PRIORITY WHERE PROJECT_TEMPLATE_ID = @NewTemplateId)
	) MAP
	ON INC.INCIDENT_ID = MAP.INCIDENT_ID
WHERE 
	MAP.INCIDENT_ID = INC.INCIDENT_ID
		
--Incident Severity
UPDATE INC
	SET INC.SEVERITY_ID = MAP.NEW_SEVERITY_ID
FROM 
	TST_INCIDENT INC INNER JOIN (
	SELECT INC.INCIDENT_ID, T1.SEVERITY_ID AS OLD_SEVERITY_ID, T2.SEVERITY_ID AS NEW_SEVERITY_ID
	FROM TST_INCIDENT INC
		INNER JOIN TST_INCIDENT_SEVERITY T1 ON INC.SEVERITY_ID = T1.SEVERITY_ID
		INNER JOIN TST_INCIDENT_SEVERITY T2 ON T1.NAME = T2.NAME		
	WHERE INC.PROJECT_ID = @ProjectId
		AND T1.PROJECT_TEMPLATE_ID = @OldTemplateId
		AND T2.PROJECT_TEMPLATE_ID = @NewTemplateId
	UNION	
	SELECT INC.INCIDENT_ID, T1.SEVERITY_ID AS OLD_SEVERITY_ID, NULL AS NEW_SEVERITY_ID
	FROM TST_INCIDENT INC
		INNER JOIN TST_INCIDENT_SEVERITY T1 ON INC.SEVERITY_ID = T1.SEVERITY_ID
	WHERE INC.PROJECT_ID = @ProjectId
		AND T1.PROJECT_TEMPLATE_ID = @OldTemplateId
		AND T1.NAME NOT IN (SELECT NAME FROM TST_INCIDENT_SEVERITY WHERE PROJECT_TEMPLATE_ID = @NewTemplateId)
	) MAP
	ON INC.INCIDENT_ID = MAP.INCIDENT_ID
WHERE 
	MAP.INCIDENT_ID = INC.INCIDENT_ID
END
GO
