<?xml version="1.0" encoding="utf-8"?>
<xsl:stylesheet version="1.0" xmlns:xsl="http://www.w3.org/1999/XSL/Transform" xmlns:msxsl="urn:schemas-microsoft-com:xslt" exclude-result-prefixes="msxsl">
  <xsl:template match="/TestRunData">
    <table class="DataGrid" style="width:100%">
      <tr>
        <th>Test Run #</th>
        <th>Name</th>
        <th>Test Case #</th>
        <xsl:if test="TestRun/TestRunSteps">
          <th>Test Step</th>
          <th>Test Step Description</th>
          <th>Test Step Expected Result</th>
          <th>Test Step Sample Data</th>
          <th>Test Step Actual Result</th>
        </xsl:if>
        <th>Status</th>
        <th>Release</th>
        <th>Build</th>
        <th>Test Set</th>
        <th>Type</th>
        <th>Tester</th>
        <th>Est. Duration</th>
        <th>Actual Duration</th>
        <th>Start Date</th>
        <th>End Date</th>
        <th>Automation Engine</th>
        <th>Automation Host</th>
        <th>Message</th>
        <xsl:for-each select="TestRun[1]/CustomProperties/CustomProperty">
          <th>
            <xsl:value-of select="Alias"/>
          </th>
        </xsl:for-each>
      </tr>
      <xsl:for-each select="TestRun">
        <tr>
          <td>
            TR<xsl:value-of select="TestRunId"/>
          </td>
          <td>
            <xsl:value-of select="Name"/>
          </td>
          <td>
            TC<xsl:value-of select="TestCaseId"/>
          </td>
          <xsl:if test="TestRunSteps">
            <td></td>
            <td></td>
            <td></td>
            <td></td>
            <td></td>
          </xsl:if>
          <td>
            <xsl:value-of select="ExecutionStatusName"/>
          </td>
          <td>
            <xsl:value-of select="ReleaseVersionNumber"/>
          </td>
          <td>
            <xsl:value-of select="BuildName"/>
          </td>
          <td>
            <xsl:value-of select="TestSetName"/>
          </td>
          <td>
            <xsl:value-of select="TestRunTypeName"/>
          </td>
          <td>
            <xsl:value-of select="TesterName"/>
          </td>
          <td class="Timespan">
            <xsl:value-of select="EstimatedDuration"/>
          </td>
          <td class="Timespan">
            <xsl:value-of select="ActualDuration"/>
          </td>
          <td class="Date">
            <xsl:call-template name="format-date">
              <xsl:with-param name="datetime" select="StartDate" />
            </xsl:call-template>
          </td>
          <td class="Date">
            <xsl:call-template name="format-date">
              <xsl:with-param name="datetime" select="EndDate" />
            </xsl:call-template>
          </td>
          <td>
            <xsl:value-of select="RunnerName"/>
          </td>
          <td>
            <xsl:value-of select="AutomationHostName"/>
          </td>
          <td>
            <xsl:value-of select="RunnerMessage"/>
          </td>
          <xsl:for-each select="CustomProperties/CustomProperty">
            <xsl:choose>
              <xsl:when test="Type='Date'">
                <td class="Date">
                  <xsl:call-template name="format-date">
                    <xsl:with-param name="datetime" select="Value" />
                  </xsl:call-template>
                </td>
              </xsl:when>
              <xsl:otherwise>
                <td>
                  <xsl:value-of select="Value" disable-output-escaping="yes"/>
                </td>
              </xsl:otherwise>
            </xsl:choose>
          </xsl:for-each>
        </tr>
        <xsl:for-each select="TestRunSteps/TestRunStep">
          <tr>
            <td>
              RS<xsl:value-of select="TestRunStepId"/>
            </td>
            <td></td>
            <td></td>
            <td>
              <xsl:value-of select="position()"/>
            </td>
            <td>
              <xsl:value-of select="Description" disable-output-escaping="yes"/>
            </td>
            <td>
              <xsl:value-of select="ExpectedResult" disable-output-escaping="yes"/>
            </td>
            <td>
              <xsl:value-of select="SampleData" disable-output-escaping="yes"/>
            </td>
            <td>
              <xsl:value-of select="ActualResult" disable-output-escaping="yes"/>
            </td>
            <td>
              <xsl:value-of select="ExecutionStatusName"/>
            </td>
            <td></td>
            <td></td>
            <td></td>
            <td></td>
            <td></td>
            <td></td>
            <td></td>
            <td></td>
            <td></td>
            <td></td>
            <td></td>
            <td></td>
            <xsl:for-each select="../../CustomProperties/CustomProperty">
              <td>
              </td>
            </xsl:for-each>
          </tr>
        </xsl:for-each>
      </xsl:for-each>
    </table>
  </xsl:template>
  <xsl:template name="format-date">
    <xsl:param name="datetime"/>
    <xsl:variable name="date" select="substring-before($datetime, 'T')" />
    <xsl:variable name="year" select="substring-before($date, '-')" />
    <xsl:variable name="month" select="substring-before(substring-after($date, '-'), '-')" />
    <xsl:variable name="day" select="substring-after(substring-after($date, '-'), '-')" />
    <xsl:variable name="time" select="substring-before(substring-after($datetime, 'T'), '.')" />
    <xsl:variable name="monthname">
      <xsl:choose>
        <xsl:when test="$month='01'">
          <xsl:value-of select="'Jan'"/>
        </xsl:when>
        <xsl:when test="$month='02'">
          <xsl:value-of select="'Feb'"/>
        </xsl:when>
        <xsl:when test="$month='03'">
          <xsl:value-of select="'Mar'"/>
        </xsl:when>
        <xsl:when test="$month='04'">
          <xsl:value-of select="'Apr'"/>
        </xsl:when>
        <xsl:when test="$month='05'">
          <xsl:value-of select="'May'"/>
        </xsl:when>
        <xsl:when test="$month='06'">
          <xsl:value-of select="'Jun'"/>
        </xsl:when>
        <xsl:when test="$month='07'">
          <xsl:value-of select="'Jul'"/>
        </xsl:when>
        <xsl:when test="$month='08'">
          <xsl:value-of select="'Aug'"/>
        </xsl:when>
        <xsl:when test="$month='09'">
          <xsl:value-of select="'Sep'"/>
        </xsl:when>
        <xsl:when test="$month='10'">
          <xsl:value-of select="'Oct'"/>
        </xsl:when>
        <xsl:when test="$month='11'">
          <xsl:value-of select="'Nov'"/>
        </xsl:when>
        <xsl:when test="$month='12'">
          <xsl:value-of select="'Dec'"/>
        </xsl:when>
        <xsl:otherwise>
          <xsl:value-of select="''" />
        </xsl:otherwise>
      </xsl:choose>
    </xsl:variable>
    <xsl:value-of select="concat($day, '-' ,$monthname, '-', $year , ' ', $time)" />
  </xsl:template>
</xsl:stylesheet>

