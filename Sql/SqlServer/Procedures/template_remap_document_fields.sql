-- =============================================
-- Author:			Inflectra Corporation
-- Business Object: Template
-- Description:		Changes the document fields in a project from one template to another
-- Remarks:			It maps field by name where possible, otherwise just uses the defaults
-- =============================================
IF OBJECT_ID ( 'TEMPLATE_REMAP_DOCUMENT_FIELDS', 'P' ) IS NOT NULL 
    DROP PROCEDURE TEMPLATE_REMAP_DOCUMENT_FIELDS;
GO
CREATE PROCEDURE TEMPLATE_REMAP_DOCUMENT_FIELDS
	@ProjectId INT,
	@OldTemplateId INT,
	@NewTemplateId INT
AS
BEGIN
	SET NOCOUNT ON;

-- Document Types	
UPDATE INC
	SET INC.DOCUMENT_TYPE_ID = MAP.NEW_TYPE_ID
FROM 
	TST_PROJECT_ATTACHMENT INC INNER JOIN (
	SELECT INC.ATTACHMENT_ID, T1.DOCUMENT_TYPE_ID AS OLD_TYPE_ID, T2.DOCUMENT_TYPE_ID AS NEW_TYPE_ID
	FROM TST_PROJECT_ATTACHMENT INC
		INNER JOIN TST_DOCUMENT_TYPE T1 ON INC.DOCUMENT_TYPE_ID = T1.DOCUMENT_TYPE_ID
		INNER JOIN TST_DOCUMENT_TYPE T2 ON T1.NAME = T2.NAME		
	WHERE INC.PROJECT_ID = @ProjectId
		AND T1.PROJECT_TEMPLATE_ID = @OldTemplateId
		AND T2.PROJECT_TEMPLATE_ID = @NewTemplateId
	UNION	
	SELECT INC.ATTACHMENT_ID, T1.DOCUMENT_TYPE_ID AS OLD_TYPE_ID, (SELECT TOP 1 DOCUMENT_TYPE_ID FROM TST_DOCUMENT_TYPE WHERE IS_DEFAULT = 1 AND PROJECT_TEMPLATE_ID = @NewTemplateId) AS NEW_TYPE_ID
	FROM TST_PROJECT_ATTACHMENT INC
		INNER JOIN TST_DOCUMENT_TYPE T1 ON INC.DOCUMENT_TYPE_ID = T1.DOCUMENT_TYPE_ID
	WHERE INC.PROJECT_ID = @ProjectId
		AND T1.PROJECT_TEMPLATE_ID = @OldTemplateId
		AND T1.NAME NOT IN (SELECT NAME FROM TST_DOCUMENT_TYPE WHERE PROJECT_TEMPLATE_ID = @NewTemplateId)
	) MAP
	ON INC.ATTACHMENT_ID = MAP.ATTACHMENT_ID
WHERE 
	MAP.ATTACHMENT_ID = INC.ATTACHMENT_ID

-- Document Status
UPDATE INC
	SET INC.DOCUMENT_STATUS_ID = MAP.NEW_STATUS_ID
FROM 
	TST_ATTACHMENT INC INNER JOIN (
	SELECT INC.ATTACHMENT_ID, T1.DOCUMENT_STATUS_ID AS OLD_STATUS_ID, T2.DOCUMENT_STATUS_ID AS NEW_STATUS_ID
	FROM TST_ATTACHMENT INC
		INNER JOIN TST_DOCUMENT_STATUS T1 ON INC.DOCUMENT_STATUS_ID = T1.DOCUMENT_STATUS_ID
		INNER JOIN TST_DOCUMENT_STATUS T2 ON T1.NAME = T2.NAME		
		INNER JOIN TST_PROJECT_ATTACHMENT PAT ON INC.ATTACHMENT_ID = PAT.ATTACHMENT_ID
	WHERE PAT.PROJECT_ID = @ProjectId
		AND T1.PROJECT_TEMPLATE_ID = @OldTemplateId
		AND T2.PROJECT_TEMPLATE_ID = @NewTemplateId
	UNION	
	SELECT INC.ATTACHMENT_ID, T1.DOCUMENT_STATUS_ID AS OLD_STATUS_ID, (SELECT TOP 1 DOCUMENT_STATUS_ID FROM TST_DOCUMENT_STATUS WHERE IS_DEFAULT = 1 AND PROJECT_TEMPLATE_ID = @NewTemplateId) AS NEW_STATUS_ID
	FROM TST_ATTACHMENT INC
		INNER JOIN TST_DOCUMENT_STATUS T1 ON INC.DOCUMENT_STATUS_ID = T1.DOCUMENT_STATUS_ID
		INNER JOIN TST_PROJECT_ATTACHMENT PAT ON INC.ATTACHMENT_ID = PAT.ATTACHMENT_ID
	WHERE PAT.PROJECT_ID = @ProjectId
		AND T1.PROJECT_TEMPLATE_ID = @OldTemplateId
		AND T1.NAME NOT IN (SELECT NAME FROM TST_DOCUMENT_STATUS WHERE PROJECT_TEMPLATE_ID = @NewTemplateId)
	) MAP
	ON INC.ATTACHMENT_ID = MAP.ATTACHMENT_ID
WHERE 
	MAP.ATTACHMENT_ID = INC.ATTACHMENT_ID	
END
GO
