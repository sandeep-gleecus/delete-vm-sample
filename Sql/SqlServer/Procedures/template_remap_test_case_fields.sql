-- =============================================
-- Author:			Inflectra Corporation
-- Business Object: Template
-- Description:		Changes the test case fields in a project from one template to another
-- Remarks:			It maps field by name where possible, otherwise just uses the defaults
-- =============================================
IF OBJECT_ID ( 'TEMPLATE_REMAP_TEST_CASE_FIELDS', 'P' ) IS NOT NULL 
    DROP PROCEDURE TEMPLATE_REMAP_TEST_CASE_FIELDS;
GO
CREATE PROCEDURE TEMPLATE_REMAP_TEST_CASE_FIELDS
	@ProjectId INT,
	@OldTemplateId INT,
	@NewTemplateId INT
AS
BEGIN
	SET NOCOUNT ON;

-- Test Case Types	
UPDATE INC
	SET INC.TEST_CASE_TYPE_ID = MAP.NEW_TYPE_ID
FROM 
	TST_TEST_CASE INC INNER JOIN (
	SELECT INC.TEST_CASE_ID, T1.TEST_CASE_TYPE_ID AS OLD_TYPE_ID, T2.TEST_CASE_TYPE_ID AS NEW_TYPE_ID
	FROM TST_TEST_CASE INC
		INNER JOIN TST_TEST_CASE_TYPE T1 ON INC.TEST_CASE_TYPE_ID = T1.TEST_CASE_TYPE_ID
		INNER JOIN TST_TEST_CASE_TYPE T2 ON T1.NAME = T2.NAME		
	WHERE INC.PROJECT_ID = @ProjectId
		AND T1.PROJECT_TEMPLATE_ID = @OldTemplateId
		AND T2.PROJECT_TEMPLATE_ID = @NewTemplateId
	UNION	
	SELECT INC.TEST_CASE_ID, T1.TEST_CASE_TYPE_ID AS OLD_TYPE_ID, (SELECT TOP 1 TEST_CASE_TYPE_ID FROM TST_TEST_CASE_TYPE WHERE IS_DEFAULT = 1 AND PROJECT_TEMPLATE_ID = @NewTemplateId) AS NEW_TYPE_ID
	FROM TST_TEST_CASE INC
		INNER JOIN TST_TEST_CASE_TYPE T1 ON INC.TEST_CASE_TYPE_ID = T1.TEST_CASE_TYPE_ID
	WHERE INC.PROJECT_ID = @ProjectId
		AND T1.PROJECT_TEMPLATE_ID = @OldTemplateId
		AND T1.NAME NOT IN (SELECT NAME FROM TST_TEST_CASE_TYPE WHERE PROJECT_TEMPLATE_ID = @NewTemplateId)
	) MAP
	ON INC.TEST_CASE_ID = MAP.TEST_CASE_ID
WHERE 
	MAP.TEST_CASE_ID = INC.TEST_CASE_ID

--Test Case Priority
UPDATE INC
	SET INC.TEST_CASE_PRIORITY_ID = MAP.NEW_TEST_CASE_PRIORITY_ID
FROM 
	TST_TEST_CASE INC INNER JOIN (
	SELECT INC.TEST_CASE_ID, T1.TEST_CASE_PRIORITY_ID AS OLD_TEST_CASE_PRIORITY_ID, T2.TEST_CASE_PRIORITY_ID AS NEW_TEST_CASE_PRIORITY_ID
	FROM TST_TEST_CASE INC
		INNER JOIN TST_TEST_CASE_PRIORITY T1 ON INC.TEST_CASE_PRIORITY_ID = T1.TEST_CASE_PRIORITY_ID
		INNER JOIN TST_TEST_CASE_PRIORITY T2 ON T1.NAME = T2.NAME		
	WHERE INC.PROJECT_ID = @ProjectId
		AND T1.PROJECT_TEMPLATE_ID = @OldTemplateId
		AND T2.PROJECT_TEMPLATE_ID = @NewTemplateId
	UNION	
	SELECT INC.TEST_CASE_ID, T1.TEST_CASE_PRIORITY_ID AS OLD_TEST_CASE_PRIORITY_ID, NULL AS NEW_TEST_CASE_PRIORITY_ID
	FROM TST_TEST_CASE INC
		INNER JOIN TST_TEST_CASE_PRIORITY T1 ON INC.TEST_CASE_PRIORITY_ID = T1.TEST_CASE_PRIORITY_ID
	WHERE INC.PROJECT_ID = @ProjectId
		AND T1.PROJECT_TEMPLATE_ID = @OldTemplateId
		AND T1.NAME NOT IN (SELECT NAME FROM TST_TEST_CASE_PRIORITY WHERE PROJECT_TEMPLATE_ID = @NewTemplateId)
	) MAP
	ON INC.TEST_CASE_ID = MAP.TEST_CASE_ID
WHERE 
	MAP.TEST_CASE_ID = INC.TEST_CASE_ID	
END
GO
