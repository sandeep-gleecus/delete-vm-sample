<?xml version="1.0" encoding="utf-8"?>
<xsl:stylesheet version="1.0" xmlns:xsl="http://www.w3.org/1999/XSL/Transform" xmlns:msxsl="urn:schemas-microsoft-com:xslt" exclude-result-prefixes="msxsl">
  <xsl:template match="/TestSetData">
    <xsl:for-each select="TestSetFolder">
      <div class="Title2">
        Folder: <xsl:value-of select="Name"/>
      </div>
      <div class="Description">
        <xsl:value-of select="Description" disable-output-escaping="yes"/>
      </div>
      <xsl:for-each select="TestSets/TestSet">
        <div class="Title3">
          Test Set TX:<xsl:value-of select="TestSetId"/> - <xsl:value-of select="Name"/>
        </div>
        <div class="Description">
          <xsl:value-of select="Description" disable-output-escaping="yes"/>
        </div>
        <table class="HiddenTable">
          <tr>
            <th>Owner:</th>
            <td>
              <xsl:value-of select="OwnerName"/>
            </td>
            <th>Creator:</th>
            <td>
              <xsl:value-of select="CreatorName"/>
            </td>
          </tr>
          <tr>
            <th>Release:</th>
            <td>
              <xsl:value-of select="ReleaseVersionNumber"/>
            </td>
            <th>Creation Date:</th>
            <td class="Date">
              <xsl:call-template name="format-date">
                <xsl:with-param name="datetime" select="CreationDate" />
              </xsl:call-template>
            </td>
          </tr>
          <tr>
            <th>Status:</th>
            <td>
              <xsl:value-of select="TestSetStatusName"/>
            </td>
            <th>Last Execution:</th>
            <td class="Date">
              <xsl:call-template name="format-date">
                <xsl:with-param name="datetime" select="ExecutionDate" />
              </xsl:call-template>
            </td>
          </tr>
          <tr>
            <th>Planned Date:</th>
            <td class="Date">
              <xsl:call-template name="format-date">
                <xsl:with-param name="datetime" select="PlannedDate" />
              </xsl:call-template>
            </td>
            <th>Last Updated:</th>
            <td class="Date">
              <xsl:call-template name="format-date">
                <xsl:with-param name="datetime" select="LastUpdateDate" />
              </xsl:call-template>
            </td>
          </tr>
          <tr>
            <th>Est. Duration:</th>
            <td class="Timespan">
              <xsl:value-of select="EstimatedDuration"/>
            </td>
            <th>Act. Duration:</th>
            <td class="Timespan">
              <xsl:value-of select="ActualDuration"/>
            </td>
          </tr>
          <tr>
            <th>Automation Host:</th>
            <td>
              <xsl:value-of select="AutomationHostName"/>
            </td>
            <th></th>
            <td>
            </td>
          </tr>
          <xsl:for-each select="CustomProperties/CustomProperty">
            <tr>
              <th>
                <xsl:value-of select="Alias"/>:
              </th>
              <xsl:choose>
                <xsl:when test="Type='Date'">
                  <td class="Date" colspan="3">
                    <xsl:call-template name="format-date">
                      <xsl:with-param name="datetime" select="Value" />
                    </xsl:call-template>
                  </td>
                </xsl:when>
                <xsl:otherwise>
                  <td colspan="3">
                    <xsl:value-of select="Value" disable-output-escaping="yes"/>
                  </td>
                </xsl:otherwise>
              </xsl:choose>
            </tr>
          </xsl:for-each>
        </table>
        <xsl:if test="Discussions/Discussion">
          <div class="Title3">
            Comments:
          </div>
          <table class="DataGrid">
            <tr>
              <th>Date</th>
              <th>Creator</th>
              <th>Comment</th>
              <th>Id</th>
            </tr>
            <xsl:for-each select="Discussions/Discussion">
              <tr>
                <td class="Date">
                  <xsl:call-template name="format-date">
                    <xsl:with-param name="datetime" select="CreationDate" />
                  </xsl:call-template>
                </td>
                <td>
                  <xsl:value-of select="CreatorName"/>
                </td>
                <td>
                  <xsl:value-of select="Text" disable-output-escaping="yes"/>
                </td>
                <td>
                  <xsl:value-of select="DiscussionId"/>
                </td>
              </tr>
            </xsl:for-each>
          </table>
        </xsl:if>
      
        <xsl:if test="Parameters/TestSetParameter">
          <div class="Title3">
            Test Set Parameter Values:
          </div>
            <ul>
              <xsl:for-each select="Parameters/TestSetParameter">
                <li>
                  <xsl:value-of select="Name"/> = <xsl:value-of select="Value"/>
                </li>
              </xsl:for-each>
            </ul>
        </xsl:if>
          
        <xsl:if test="TestCases/TestCase">
          <div class="Title3">
            Test Cases:
          </div>
          <table class="DataGrid">
            <tr>
              <th>Test #</th>
              <th>Name</th>
              <th>Parameters</th>
              <th>Owner</th>
              <th>Priority</th>
              <th>Est. Duration</th>
              <th>Act. Duration</th>
              <th>Status</th>
              <th>Last Execution Date</th>
            </tr>
            <xsl:for-each select="TestCases/TestCase">
              <tr>
                <td>
                  TC<xsl:value-of select="TestCaseId"/>
                </td>
                <td>
                  <xsl:value-of select="Name"/>
                </td>
                <td>
                  <xsl:for-each select="Parameters">
                    <ul>
                      <xsl:for-each select="TestSetTestCaseParameter">
                        <li>
                          <xsl:value-of select="Name"/> = <xsl:value-of select="Value"/>
                        </li>
                      </xsl:for-each>
                    </ul>
                  </xsl:for-each>
                </td>
                <td>
                  <xsl:value-of select="OwnerName"/>
                </td>
                <td>
                  <xsl:value-of select="TestCasePriorityName"/>
                </td>
                <td class="Timespan">
                  <xsl:value-of select="EstimatedDuration"/>
                </td>
                <td class="Timespan">
                  <xsl:value-of select="ActualDuration"/>
                </td>
                <td>
                  <xsl:value-of select="ExecutionStatusName"/>
                </td>
                <td class="Date">
                  <xsl:call-template name="format-date">
                    <xsl:with-param name="datetime" select="ExecutionDate" />
                  </xsl:call-template>
                </td>
              </tr>
              <xsl:if test="TestSteps/TestStep">
                <tr>
                  <td colspan="9">
                    <table class="DataGrid">
                      <tr>
                        <th>Step</th>
                        <th>Description</th>
                        <th>Expected Result</th>
                        <th>Sample Data</th>
                        <th>Status</th>
                      </tr>
                      <xsl:for-each select="TestSteps/TestStep">
                        <tr>
                          <td>
                            <xsl:value-of select="position()"/>
                          </td>
                          <xsl:if test="string(LinkedTestCaseId)">
                            <td colspan="3">
                              <xsl:value-of select="Description" disable-output-escaping="yes"/>
                              '<xsl:value-of select="LinkedTestCaseName"/>'
                              <xsl:for-each select="Parameters">
                                <i>
                                  with
                                  <xsl:for-each select="TestStepParameter">
                                    <xsl:value-of select="Name"/> = '<xsl:value-of select="Value"/>',
                                  </xsl:for-each>
                                </i>
                              </xsl:for-each>
                            </td>
                          </xsl:if>
                          <xsl:if test="not(string(LinkedTestCaseId))">
                            <td>
                              <xsl:value-of select="Description" disable-output-escaping="yes"/>
                            </td>
                            <td>
                              <xsl:value-of select="ExpectedResult" disable-output-escaping="yes"/>
                            </td>
                            <td>
                              <xsl:value-of select="SampleData" disable-output-escaping="yes"/>
                            </td>
                          </xsl:if>
                          <td>
                            <xsl:value-of select="ExecutionStatusName"/>
                          </td>
                        </tr>
                      </xsl:for-each>
                    </table>
                  </td>
                </tr>
              </xsl:if>
            </xsl:for-each>
          </table>
        </xsl:if>
        <xsl:if test="TestRuns/TestRun">
          <div class="Title3">
            Test Runs:
          </div>
          <table class="DataGrid">
            <tr>
              <th>Run #</th>
              <th>Name</th>
              <th>Tester</th>
              <th>Release</th>
              <th>Version</th>
              <th>Status</th>
              <th>Type</th>
              <th>Automation Host</th>
              <th>Automation Engine</th>
              <th>Est. Duration</th>
              <th>Actual Duration</th>
              <th>Execution Date</th>
            </tr>
            <xsl:for-each select="TestRuns/TestRun">
              <tr>
                <td>
                  TR<xsl:value-of select="TestRunId"/>
                </td>
                <td>
                  <xsl:value-of select="Name"/>
                </td>
                <td>
                  <xsl:value-of select="TesterName"/>
                </td>
                <td>
                  <xsl:value-of select="ReleaseName"/>
                </td>
                <td>
                  <xsl:value-of select="ReleaseVersionNumber"/>
                </td>
                <td>
                  <xsl:value-of select="ExecutionStatusName"/>
                </td>
                <td>
                  <xsl:value-of select="TestRunTypeName"/>
                </td>
                <td>
                  <xsl:value-of select="AutomationHostName"/>
                </td>
                <td>
                  <xsl:value-of select="RunnerName"/>
                </td>
                <td class="Timespan">
                  <xsl:value-of select="EstimatedDuration"/>
                </td>
                <td class="Timespan">
                  <xsl:value-of select="ActualDuration"/>
                </td>
                <td class="Date">
                  <xsl:call-template name="format-date">
                    <xsl:with-param name="datetime" select="EndDate" />
                  </xsl:call-template>
                </td>
              </tr>
              <xsl:if test="TestRunSteps/TestRunStep">
                <tr>
                  <td colspan="12">
                    <table class="DataGrid">
                      <tr>
                        <th>Step</th>
                        <th>Description</th>
                        <th>Expected Result</th>
                        <th>Sample Data</th>
                        <th>ActualResult</th>
                        <th>Status</th>
                      </tr>
                      <xsl:for-each select="TestRunSteps/TestRunStep">
                        <tr>
                          <td>
                            <xsl:value-of select="Position"/>
                          </td>
                          <td>
                            <xsl:value-of select="Description" disable-output-escaping="yes"/>
                          </td>
                          <td>
                            <xsl:value-of select="ExpectedResult" disable-output-escaping="yes"/>
                          </td>
                          <td>
                            <xsl:value-of select="SampleData" disable-output-escaping="yes"/>
                          </td>
                          <td>
                            <xsl:value-of select="ActualResult" disable-output-escaping="yes"/>
                          </td>
                          <td>
                            <xsl:value-of select="ExecutionStatusName"/>
                          </td>
                        </tr>
                      </xsl:for-each>
                    </table>
                  </td>
                </tr>
              </xsl:if>
            </xsl:for-each>
          </table>
        </xsl:if>
        <xsl:if test="Attachments/Attachment">
          <div class="Title3">
            File Attachments:
          </div>
          <table class="DataGrid">
            <tr>
              <th>Filename</th>
              <th>Description</th>
              <th>Author</th>
              <th>Date Uploaded</th>
            </tr>
            <xsl:for-each select="Attachments/Attachment">
              <tr>
                <td>
                  <xsl:value-of select="Filename"/>
                </td>
                <td>
                  <xsl:value-of select="Description"/>
                </td>
                <td>
                  <xsl:value-of select="AuthorName"/>
                </td>
                <td class="Date">
                  <xsl:call-template name="format-date">
                    <xsl:with-param name="datetime" select="UploadDate" />
                  </xsl:call-template>
                </td>
              </tr>
            </xsl:for-each>
          </table>
        </xsl:if>
        <xsl:if test="History/ArtifactHistory">
          <div class="Title3">
            Test Set Change History:
          </div>
          <table class="DataGrid">
            <tr>
              <th>Change Date</th>
              <th>Field Name</th>
              <th>Old Value</th>
              <th>New Value</th>
              <th>Changed By</th>
            </tr>
            <xsl:for-each select="History/ArtifactHistory">
              <tr>
                <td class="Date">
                  <xsl:call-template name="format-date">
                    <xsl:with-param name="datetime" select="ChangeDate" />
                  </xsl:call-template>
                </td>
                <td>
                  <xsl:value-of select="FieldCaption"/>
                </td>
                <td>
                  <xsl:value-of select="OldValue" disable-output-escaping="yes"/>
                </td>
                <td>
                  <xsl:value-of select="NewValue" disable-output-escaping="yes"/>
                </td>
                <td>
                  <xsl:value-of select="ChangerName"/>
                </td>
              </tr>
            </xsl:for-each>
          </table>
        </xsl:if>
        <br/>
      </xsl:for-each>
    </xsl:for-each>
  </xsl:template>
  <xsl:template name="format-date">
    <xsl:param name="datetime"/>
    <xsl:variable name="date" select="substring-before($datetime, 'T')" />
    <xsl:variable name="year" select="substring-before($date, '-')" />
    <xsl:variable name="month" select="substring-before(substring-after($date, '-'), '-')" />
    <xsl:variable name="day" select="substring-after(substring-after($date, '-'), '-')" />
    <xsl:variable name="time" select="substring-before(substring-after($datetime, 'T'), '.')" />
    <xsl:variable name="monthname">
      <xsl:choose>
        <xsl:when test="$month='01'">
          <xsl:value-of select="'Jan'"/>
        </xsl:when>
        <xsl:when test="$month='02'">
          <xsl:value-of select="'Feb'"/>
        </xsl:when>
        <xsl:when test="$month='03'">
          <xsl:value-of select="'Mar'"/>
        </xsl:when>
        <xsl:when test="$month='04'">
          <xsl:value-of select="'Apr'"/>
        </xsl:when>
        <xsl:when test="$month='05'">
          <xsl:value-of select="'May'"/>
        </xsl:when>
        <xsl:when test="$month='06'">
          <xsl:value-of select="'Jun'"/>
        </xsl:when>
        <xsl:when test="$month='07'">
          <xsl:value-of select="'Jul'"/>
        </xsl:when>
        <xsl:when test="$month='08'">
          <xsl:value-of select="'Aug'"/>
        </xsl:when>
        <xsl:when test="$month='09'">
          <xsl:value-of select="'Sep'"/>
        </xsl:when>
        <xsl:when test="$month='10'">
          <xsl:value-of select="'Oct'"/>
        </xsl:when>
        <xsl:when test="$month='11'">
          <xsl:value-of select="'Nov'"/>
        </xsl:when>
        <xsl:when test="$month='12'">
          <xsl:value-of select="'Dec'"/>
        </xsl:when>
        <xsl:otherwise>
          <xsl:value-of select="''" />
        </xsl:otherwise>
      </xsl:choose>
    </xsl:variable>
    <xsl:value-of select="concat($day, '-' ,$monthname, '-', $year , ' ', $time)" />
  </xsl:template>
</xsl:stylesheet>

