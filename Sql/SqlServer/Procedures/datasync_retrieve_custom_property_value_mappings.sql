-- =====================================================================
-- Author:			Inflectra Corporation
-- Business Object: DataSyncManager
-- Description:		Retrieves a dataset containing the list of custom property values and their mappings
-- =====================================================================
IF OBJECT_ID ( 'DATA_SYNC_RETRIEVE_CUSTOM_PROPERTY_VALUE_MAPPINGS', 'P' ) IS NOT NULL 
    DROP PROCEDURE [DATA_SYNC_RETRIEVE_CUSTOM_PROPERTY_VALUE_MAPPINGS];
GO
CREATE PROCEDURE [DATA_SYNC_RETRIEVE_CUSTOM_PROPERTY_VALUE_MAPPINGS]
	@DataSyncSystemId INT,
	@ProjectId INT,
	@CustomListId INT,
	@IncludeUnmapped BIT
AS
BEGIN
	IF @IncludeUnmapped = 1
	BEGIN
		SELECT	@DataSyncSystemId AS DATA_SYNC_SYSTEM_ID, @ProjectId AS PROJECT_ID, CPV.CUSTOM_PROPERTY_VALUE_ID,
               CVM.EXTERNAL_KEY, CPV.NAME AS CUSTOM_PROPERTY_VALUE_NAME, CPV.IS_ACTIVE
		FROM	(SELECT * FROM TST_DATA_SYNC_CUSTOM_PROPERTY_VALUE_MAPPING
				WHERE DATA_SYNC_SYSTEM_ID = @DataSyncSystemId
				AND PROJECT_ID = @ProjectId) CVM RIGHT JOIN TST_CUSTOM_PROPERTY_VALUE CPV
		ON     CVM.CUSTOM_PROPERTY_VALUE_ID = CPV.CUSTOM_PROPERTY_VALUE_ID
		WHERE  CPV.CUSTOM_PROPERTY_LIST_ID = @CustomListId AND CPV.IS_DELETED = 0
		ORDER BY CPV.NAME
	END
	ELSE
	BEGIN
		SELECT	CVM.DATA_SYNC_SYSTEM_ID, CVM.PROJECT_ID, CVM.CUSTOM_PROPERTY_VALUE_ID,
               CVM.EXTERNAL_KEY, CPV.NAME AS CUSTOM_PROPERTY_VALUE_NAME, CPV.IS_ACTIVE
		FROM	TST_DATA_SYNC_CUSTOM_PROPERTY_VALUE_MAPPING CVM INNER JOIN TST_CUSTOM_PROPERTY_VALUE CPV
		ON     CVM.CUSTOM_PROPERTY_VALUE_ID = CPV.CUSTOM_PROPERTY_VALUE_ID
		WHERE  CVM.DATA_SYNC_SYSTEM_ID = @DataSyncSystemId
		AND    CVM.PROJECT_ID = @ProjectId
		AND    CPV.CUSTOM_PROPERTY_LIST_ID = @CustomListId
		ORDER BY CPV.NAME
	END
END
GO
