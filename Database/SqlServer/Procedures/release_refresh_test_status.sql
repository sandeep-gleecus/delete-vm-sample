-- =============================================
-- Author:			Inflectra Corporation
-- Business Object: Release
-- Description:		Refreshes the test status of a particular release
-- =============================================
IF OBJECT_ID ( 'RELEASE_REFRESH_TEST_STATUS', 'P' ) IS NOT NULL 
    DROP PROCEDURE RELEASE_REFRESH_TEST_STATUS;
GO
CREATE PROCEDURE RELEASE_REFRESH_TEST_STATUS
	@ProjectId INT,
	@ReleaseId INT
AS
BEGIN
	MERGE TST_RELEASE AS TARGET	
	USING (
		SELECT
			RTC.RELEASE_ID,
			SUM(CASE RTC.EXECUTION_STATUS_ID WHEN 1 THEN 1 ELSE 0 END) AS COUNT_FAILED,
			SUM(CASE RTC.EXECUTION_STATUS_ID WHEN 2 THEN 1 ELSE 0 END) AS COUNT_PASSED,
			SUM(CASE RTC.EXECUTION_STATUS_ID WHEN 3 THEN 1 ELSE 0 END) AS COUNT_NOT_RUN,
			SUM(CASE RTC.EXECUTION_STATUS_ID WHEN 4 THEN 1 ELSE 0 END) AS COUNT_NOT_APPLICABLE,
			SUM(CASE RTC.EXECUTION_STATUS_ID WHEN 5 THEN 1 ELSE 0 END) AS COUNT_BLOCKED,
			SUM(CASE RTC.EXECUTION_STATUS_ID WHEN 6 THEN 1 ELSE 0 END) AS COUNT_CAUTION
		FROM
			TST_RELEASE_TEST_CASE RTC INNER JOIN TST_TEST_CASE TST
			ON RTC.TEST_CASE_ID = TST.TEST_CASE_ID
		WHERE
			RTC.RELEASE_ID = @ReleaseId AND
			TST.IS_DELETED = 0
		GROUP BY RELEASE_ID
	) AS SOURCE
	ON
		TARGET.RELEASE_ID = SOURCE.RELEASE_ID
	WHEN MATCHED THEN
		UPDATE
			SET	
				TARGET.COUNT_PASSED = SOURCE.COUNT_PASSED,
				TARGET.COUNT_FAILED = SOURCE.COUNT_FAILED,
				TARGET.COUNT_CAUTION = SOURCE.COUNT_CAUTION,
				TARGET.COUNT_BLOCKED = SOURCE.COUNT_BLOCKED,
				TARGET.COUNT_NOT_RUN = SOURCE.COUNT_NOT_RUN,
				TARGET.COUNT_NOT_APPLICABLE = SOURCE.COUNT_NOT_APPLICABLE
	WHEN NOT MATCHED BY SOURCE AND TARGET.RELEASE_ID = @ReleaseId THEN
			UPDATE
			SET	
				TARGET.COUNT_PASSED = 0,
				TARGET.COUNT_FAILED = 0,
				TARGET.COUNT_CAUTION = 0,
				TARGET.COUNT_BLOCKED = 0,
				TARGET.COUNT_NOT_RUN = 0,
				TARGET.COUNT_NOT_APPLICABLE = 0;
END
GO
