-- ====================================================================================
-- Author:			Inflectra Corporation
-- Business Object: TestCase
-- Description:		Retrieves a list of test-case execution status summary for an entire project group
-- =====================================================================================
IF OBJECT_ID ( 'TESTCASE_RETRIEVE_EXECUTION_STATUS_SUMMARY_GROUP', 'P' ) IS NOT NULL 
    DROP PROCEDURE [TESTCASE_RETRIEVE_EXECUTION_STATUS_SUMMARY_GROUP];
GO
CREATE PROCEDURE [TESTCASE_RETRIEVE_EXECUTION_STATUS_SUMMARY_GROUP]
	@ProjectGroupId INT,
	@ActiveReleasesOnly BIT
AS
BEGIN
	IF @ActiveReleasesOnly = 1
	BEGIN
		SELECT	EXE.EXECUTION_STATUS_ID AS EXECUTION_STATUS_ID,
				MIN(EXE.NAME) AS EXECUTION_STATUS_NAME,
				COUNT(TST.TEST_CASE_ID) AS STATUS_COUNT
		FROM	TST_EXECUTION_STATUS EXE
		LEFT JOIN (SELECT RTC.TEST_CASE_ID, RTC.EXECUTION_STATUS_ID
					FROM TST_RELEASE_TEST_CASE RTC
					INNER JOIN TST_TEST_CASE TST ON TST.TEST_CASE_ID = RTC.TEST_CASE_ID
					INNER JOIN TST_PROJECT PRJ ON TST.PROJECT_ID = PRJ.PROJECT_ID
					INNER JOIN TST_RELEASE REL ON RTC.RELEASE_ID = REL.RELEASE_ID
					WHERE PRJ.IS_ACTIVE = 1
						AND REL.RELEASE_STATUS_ID NOT IN (4 /*Closed*/, 5 /*Deferred*/, 6 /*Cancelled*/)
						AND REL.IS_DELETED = 0
						AND PRJ.PROJECT_GROUP_ID = @ProjectGroupId
						AND TST.IS_DELETED = 0) AS TST
		ON		EXE.EXECUTION_STATUS_ID = TST.EXECUTION_STATUS_ID
		WHERE	EXE.EXECUTION_STATUS_ID <> 4 /* N/A */
		GROUP BY EXE.EXECUTION_STATUS_ID
		ORDER BY EXECUTION_STATUS_ID
	END
	ELSE
	BEGIN
		SELECT	EXE.EXECUTION_STATUS_ID AS EXECUTION_STATUS_ID, MIN(EXE.NAME) AS EXECUTION_STATUS_NAME, COUNT(TST.TEST_CASE_ID) AS STATUS_COUNT
		FROM	TST_EXECUTION_STATUS EXE LEFT JOIN
			(SELECT TEST_CASE_ID, EXECUTION_STATUS_ID
				FROM TST_TEST_CASE TST2 INNER JOIN TST_PROJECT PRJ
				ON TST2.PROJECT_ID = PRJ.PROJECT_ID
				WHERE PRJ.IS_ACTIVE = 1 AND PRJ.PROJECT_GROUP_ID = @ProjectGroupId
				AND TST2.IS_DELETED = 0) TST
		ON		EXE.EXECUTION_STATUS_ID = TST.EXECUTION_STATUS_ID
		WHERE	EXE.EXECUTION_STATUS_ID <> 4 /* N/A */
		GROUP BY EXE.EXECUTION_STATUS_ID
		ORDER BY EXE.EXECUTION_STATUS_ID
	END
END
GO
