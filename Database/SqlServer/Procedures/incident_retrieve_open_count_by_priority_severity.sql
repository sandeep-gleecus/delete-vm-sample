-- =====================================================================
-- Author:			Inflectra Corporation
-- Business Object: Incident
-- Description:		Retrieves a summary count of open incidents by either priority or severity
-- =====================================================================
IF OBJECT_ID ( 'INCIDENT_RETRIEVE_OPEN_COUNT_BY_PRIORITY_SEVERITY', 'P' ) IS NOT NULL 
    DROP PROCEDURE INCIDENT_RETRIEVE_OPEN_COUNT_BY_PRIORITY_SEVERITY;
GO
CREATE PROCEDURE INCIDENT_RETRIEVE_OPEN_COUNT_BY_PRIORITY_SEVERITY
	@ProjectId INT,
	@ReleaseId INT,
	@UseSeverity BIT,
	@UseResolvedRelease BIT
AS
BEGIN
	--Handle the case where no release is specified separately
	IF @ReleaseId IS NULL
	BEGIN
		IF @UseSeverity = 1
		BEGIN
			SELECT COUNT(INC.INCIDENT_ID) AS Count, ISNULL(SEV.NAME,'(None)') AS PrioritySeverityName, SEV.COLOR AS PrioritySeverityColor
			FROM TST_INCIDENT INC INNER JOIN TST_INCIDENT_STATUS IST
			ON INC.INCIDENT_STATUS_ID = IST.INCIDENT_STATUS_ID LEFT JOIN TST_INCIDENT_SEVERITY SEV
			ON INC.SEVERITY_ID = SEV.SEVERITY_ID
			WHERE IST.IS_OPEN_STATUS = 1
			AND INC.PROJECT_ID = @ProjectId
			AND INC.IS_DELETED = 0
			GROUP BY SEV.NAME, SEV.COLOR
			ORDER BY SEV.NAME
		END
		ELSE
		BEGIN
			SELECT COUNT(INC.INCIDENT_ID) AS Count, ISNULL(PRI.NAME,'(None)') AS PrioritySeverityName, PRI.COLOR AS PrioritySeverityColor
			FROM TST_INCIDENT INC INNER JOIN TST_INCIDENT_STATUS IST
			ON INC.INCIDENT_STATUS_ID = IST.INCIDENT_STATUS_ID LEFT JOIN TST_INCIDENT_PRIORITY PRI
			ON INC.PRIORITY_ID = PRI.PRIORITY_ID
			WHERE IST.IS_OPEN_STATUS = 1
			AND INC.PROJECT_ID = @ProjectId
			AND INC.IS_DELETED = 0
			GROUP BY PRI.NAME, PRI.COLOR
			ORDER BY PRI.NAME
		END
	END
	ELSE
	BEGIN
		--Declare results set
		DECLARE  @ReleaseList TABLE
		(
			RELEASE_ID INT
		)

		--Populate list of child iterations
		INSERT @ReleaseList (RELEASE_ID)
		SELECT RELEASE_ID FROM FN_RELEASE_GET_SELF_AND_ITERATIONS (@ProjectId, @ReleaseId)

		IF @UseSeverity = 1
		BEGIN
			SELECT COUNT(INC.INCIDENT_ID) AS Count, ISNULL(SEV.NAME,'(None)') AS PrioritySeverityName, SEV.COLOR AS PrioritySeverityColor
			FROM TST_INCIDENT INC INNER JOIN TST_INCIDENT_STATUS IST
			ON INC.INCIDENT_STATUS_ID = IST.INCIDENT_STATUS_ID LEFT JOIN TST_INCIDENT_SEVERITY SEV
			ON INC.SEVERITY_ID = SEV.SEVERITY_ID
			WHERE IST.IS_OPEN_STATUS = 1
			AND INC.PROJECT_ID = @ProjectId
			AND INC.IS_DELETED = 0
			AND ((@UseResolvedRelease = 0 AND DETECTED_RELEASE_ID IN (SELECT RELEASE_ID FROM @ReleaseList))
				OR (@UseResolvedRelease = 1 AND RESOLVED_RELEASE_ID IN (SELECT RELEASE_ID FROM @ReleaseList)))
			GROUP BY SEV.NAME, SEV.COLOR
			ORDER BY SEV.NAME
		END
		ELSE
		BEGIN
			SELECT COUNT(INC.INCIDENT_ID) AS Count, ISNULL(PRI.NAME,'(None)') AS PrioritySeverityName, PRI.COLOR AS PrioritySeverityColor
			FROM TST_INCIDENT INC INNER JOIN TST_INCIDENT_STATUS IST
			ON INC.INCIDENT_STATUS_ID = IST.INCIDENT_STATUS_ID LEFT JOIN TST_INCIDENT_PRIORITY PRI
			ON INC.PRIORITY_ID = PRI.PRIORITY_ID
			WHERE IST.IS_OPEN_STATUS = 1
			AND INC.PROJECT_ID = @ProjectId
			AND INC.IS_DELETED = 0
			AND ((@UseResolvedRelease = 0 AND DETECTED_RELEASE_ID IN (SELECT RELEASE_ID FROM @ReleaseList))
				OR (@UseResolvedRelease = 1 AND RESOLVED_RELEASE_ID IN (SELECT RELEASE_ID FROM @ReleaseList)))
			GROUP BY PRI.NAME, PRI.COLOR
			ORDER BY PRI.NAME
		END
	END
END
GO
