-- =====================================================================
-- Author:			Inflectra Corporation
-- Business Object: TestCase
-- Description:		Refreshes the folder hierarchy for the specified project
-- =====================================================================
IF OBJECT_ID ( 'TESTCASE_REFRESH_FOLDER_HIERARCHY', 'P' ) IS NOT NULL 
    DROP PROCEDURE [TESTCASE_REFRESH_FOLDER_HIERARCHY];
GO
CREATE PROCEDURE [TESTCASE_REFRESH_FOLDER_HIERARCHY]
	@ProjectId INT
AS
BEGIN
	--First delete the existing folders
	DELETE FROM TST_TEST_CASE_FOLDER_HIERARCHY
	WHERE PROJECT_ID = @ProjectId;

	WITH TEST_CASE_FOLDER_HIERARCHY (TEST_CASE_FOLDER_ID, PROJECT_ID, NAME, PARENT_TEST_CASE_FOLDER_ID, HIERARCHY_LEVEL, INDENT_LEVEL)
	AS
	(
		SELECT	TKF.TEST_CASE_FOLDER_ID, TKF.PROJECT_ID, TKF.NAME, TKF.PARENT_TEST_CASE_FOLDER_ID, 1 AS HIERARCHY_LEVEL,
				CAST(dbo.FN_CREATE_INDENT_LEVEL(ROW_NUMBER() OVER(ORDER BY TKF.NAME)) AS NVARCHAR(MAX)) AS INDENT_LEVEL
		FROM TST_TEST_CASE_FOLDER TKF
		WHERE TKF.PARENT_TEST_CASE_FOLDER_ID IS NULL AND TKF.PROJECT_ID = @ProjectId
		UNION ALL
		SELECT	TKF.TEST_CASE_FOLDER_ID, TKF.PROJECT_ID, TKF.NAME, TKF.PARENT_TEST_CASE_FOLDER_ID, (CTE.HIERARCHY_LEVEL + 1) AS HIERARCHY_LEVEL,
				CTE.INDENT_LEVEL + dbo.FN_CREATE_INDENT_LEVEL(ROW_NUMBER() OVER(ORDER BY TKF.NAME)) AS INDENT_LEVEL
		FROM TST_TEST_CASE_FOLDER TKF
		INNER JOIN TEST_CASE_FOLDER_HIERARCHY CTE ON TKF.PARENT_TEST_CASE_FOLDER_ID = CTE.TEST_CASE_FOLDER_ID
		WHERE TKF.PARENT_TEST_CASE_FOLDER_ID IS NOT NULL AND TKF.PROJECT_ID = @ProjectId
	)
	INSERT INTO TST_TEST_CASE_FOLDER_HIERARCHY (TEST_CASE_FOLDER_ID, PROJECT_ID, NAME, PARENT_TEST_CASE_FOLDER_ID, HIERARCHY_LEVEL, INDENT_LEVEL)
	SELECT ISNULL(TEST_CASE_FOLDER_ID, 0) AS TEST_CASE_FOLDER_ID, PROJECT_ID, NAME, PARENT_TEST_CASE_FOLDER_ID, HIERARCHY_LEVEL, INDENT_LEVEL
	FROM TEST_CASE_FOLDER_HIERARCHY
	ORDER BY PROJECT_ID, INDENT_LEVEL COLLATE Latin1_General_BIN
END
GO
