SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO

IF OBJECT_ID('[dbo].[VM_Report_ExpectedResults]', 'P') IS NOT NULL 
	DROP PROCEDURE [dbo].[VM_Report_ExpectedResults]
GO

-- =============================================
-- Author:		Gerald Green
-- Create date: 2021.03.30
-- Description:	Build a dataset that replaces the Attachment_ID with the Attachment_Version_ID in the EXPECTED_RESULT
-- =============================================
CREATE PROCEDURE VM_Report_ExpectedResults
	@TestCaseId INT = 0
AS
BEGIN
	SET NOCOUNT ON;

	DECLARE @ExptectResutsTable TABLE(
		TST_TEST_CASE_TEST_CASE_ID INT, 
		TST_TEST_STEP_TEST_CASE_ID INT, 
		POSITION INT, 
		TEST_STEP_ID INT, 
		DESCRIPTION NVARCHAR(MAX), 
		ATTACHMENT_VERSION_ID NVARCHAR(50), 
		ATTACHMENT_ID NVARCHAR(50),
		EXPECTED_RESULT_Original NVARCHAR(MAX),
		EXPECTED_RESULT NVARCHAR(MAX))
 
	INSERT INTO @ExptectResutsTable 
		SELECT DISTINCT dbo.TST_TEST_CASE.TEST_CASE_ID as TST_TEST_CASE_TEST_CASE_ID, 
		   dbo.TST_TEST_STEP.TEST_CASE_ID as TST_TEST_STEP_TEST_CASE_ID, 
		   dbo.TST_TEST_STEP.POSITION as POSITION, 
		   dbo.TST_TEST_STEP.TEST_STEP_ID, 
		   dbo.TST_TEST_STEP.DESCRIPTION, 
		   0,0,
		   EXPECTED_RESULT AS EXPECTED_RESULT_Original,
		   EXPECTED_RESULT
		FROM dbo.TST_TEST_CASE 
		INNER JOIN dbo.TST_TEST_STEP ON dbo.TST_TEST_CASE.TEST_CASE_ID = dbo.TST_TEST_STEP.TEST_CASE_ID 
		WHERE(dbo.TST_TEST_STEP.TEST_CASE_ID = @TestCaseId)


-- EXPECTED RESULTS
	UPDATE @ExptectResutsTable
	SET ATTACHMENT_ID = SUBSTRING(EXPECTED_RESULT, PATINDEX('%Attachment/%', EXPECTED_RESULT)+11, (PATINDEX('%.aspx%', EXPECTED_RESULT)) - (PATINDEX('%Attachment/%', EXPECTED_RESULT)+11))
	WHERE EXPECTED_RESULT LIKE '%img src%'

	UPDATE @ExptectResutsTable
	SET ATTACHMENT_VERSION_ID = moo.ATTACHMENT_VERSION_ID
	FROM @ExptectResutsTable ert INNER JOIN TST_ATTACHMENT_VERSION moo ON ert.ATTACHMENT_ID = moo.ATTACHMENT_ID AND IS_CURRENT = 1

	UPDATE @ExptectResutsTable
	SET EXPECTED_RESULT = REPLACE(EXPECTED_RESULT,ATTACHMENT_ID, ATTACHMENT_VERSION_ID)
	WHERE ATTACHMENT_ID > 0

	SELECT * FROM @ExptectResutsTable
	ORDER BY POSITION
END
GO
