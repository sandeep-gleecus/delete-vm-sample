IF OBJECT_ID ( 'VW_PROJECT_USER', 'V' ) IS NOT NULL 
    DROP VIEW [VW_PROJECT_USER];
GO
CREATE VIEW [VW_PROJECT_USER]
AS
SELECT	PRU.USER_ID, PRU.PROJECT_ID, PRU.PROJECT_ROLE_ID, PRJ.NAME AS PROJECT_NAME,
		(RTRIM(UPL.FIRST_NAME + ' ' + ISNULL(UPL.MIDDLE_INITIAL,'')) + ' ' + UPL.LAST_NAME) AS FULL_NAME,
		USR.USER_NAME, PRR.NAME AS PROJECT_ROLE_NAME,
		UPL.FIRST_NAME, UPL.MIDDLE_INITIAL, UPL.LAST_NAME, USR.LDAP_DN, USR.EMAIL_ADDRESS, PRR.IS_ADMIN, USR.IS_ACTIVE, PRR.IS_TEMPLATE_ADMIN,
		PRJ.PROJECT_TEMPLATE_ID
FROM	TST_PROJECT PRJ INNER JOIN TST_PROJECT_USER PRU
ON		PRJ.PROJECT_ID = PRU.PROJECT_ID INNER JOIN TST_USER USR
ON		PRU.USER_ID = USR.USER_ID INNER JOIN TST_PROJECT_ROLE PRR
ON		PRU.PROJECT_ROLE_ID = PRR.PROJECT_ROLE_ID INNER JOIN TST_USER_PROFILE UPL
ON		USR.USER_ID = UPL.USER_ID
WHERE	PRJ.IS_ACTIVE = 1
UNION ALL
SELECT	PGU.USER_ID, PRJ.PROJECT_ID, PRR.PROJECT_ROLE_ID, PRJ.NAME AS PROJECT_NAME,
		(RTRIM(UPL.FIRST_NAME + ' ' + ISNULL(UPL.MIDDLE_INITIAL,'')) + ' ' + UPL.LAST_NAME) AS FULL_NAME,
		USR.USER_NAME, PRR.NAME AS PROJECT_ROLE_NAME,
		UPL.FIRST_NAME, UPL.MIDDLE_INITIAL, UPL.LAST_NAME, USR.LDAP_DN, USR.EMAIL_ADDRESS, CAST (0 AS BIT) AS IS_ADMIN, USR.IS_ACTIVE, CAST (0 AS BIT) AS IS_TEMPLATE_ADMIN,
		PRJ.PROJECT_TEMPLATE_ID
FROM	TST_PROJECT PRJ INNER JOIN TST_PROJECT_GROUP_USER PGU
ON		PRJ.PROJECT_GROUP_ID = PGU.PROJECT_GROUP_ID INNER JOIN TST_USER USR
ON		PGU.USER_ID = USR.USER_ID INNER JOIN TST_PROJECT_ROLE PRR
ON		PRR.PROJECT_ROLE_ID = 5 INNER JOIN TST_USER_PROFILE UPL
ON		USR.USER_ID = UPL.USER_ID
WHERE	PRJ.IS_ACTIVE = 1
AND		PGU.USER_ID NOT IN (SELECT USER_ID FROM TST_PROJECT_USER PRU2 WHERE PRU2.PROJECT_ID = PRJ.PROJECT_ID)
GO

