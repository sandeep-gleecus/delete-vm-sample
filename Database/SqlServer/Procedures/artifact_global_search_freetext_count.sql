-- =====================================================================
-- Author:			Inflectra Corporation
-- Business Object: ArtifactManager
-- Description:		Counts the VW_ARTIFACT_LIST view for items, also filtering by user project/artifact-type
-- =====================================================================
IF OBJECT_ID ( 'ARTIFACT_GLOBAL_SEARCH_FREETEXT_COUNT', 'P' ) IS NOT NULL 
    DROP PROCEDURE ARTIFACT_GLOBAL_SEARCH_FREETEXT_COUNT;
GO
CREATE PROCEDURE ARTIFACT_GLOBAL_SEARCH_FREETEXT_COUNT
	@SearchString NVARCHAR(4000),
	@ProjectArtifactList NVARCHAR(MAX)
AS
BEGIN
	--Dummy Proc Definition
	SELECT 1 FROM DUMMY
END
GO
--See if Free Text Indexing is Installed
IF (1 = FULLTEXTSERVICEPROPERTY('IsFullTextInstalled'))
BEGIN
	EXEC('
ALTER PROCEDURE ARTIFACT_GLOBAL_SEARCH_FREETEXT_COUNT
	@SearchString NVARCHAR(4000),
	@ProjectArtifactList NVARCHAR(MAX)
AS
BEGIN
	--Declare
	DECLARE @ProjectArtifactTable TABLE
	(
		PROJECT_ID INT,
		ARTIFACT_TYPE_ID INT
	)

	IF @ProjectArtifactList <> '''' AND @ProjectArtifactList IS NOT NULL
	BEGIN
		--Populate
		INSERT @ProjectArtifactTable (PROJECT_ID, ARTIFACT_TYPE_ID)
		SELECT PROJECT_ID, ARTIFACT_TYPE_ID FROM FN_ARTIFACT_PROJECTS_TYPES_TO_TABLE(@ProjectArtifactList)
	END
	
	SELECT	COUNT(*) AS SEARCH_COUNT
	FROM
	(
		SELECT	1 AS ARTIFACT_TYPE_ID, ART.REQUIREMENT_ID AS ARTIFACT_ID, ART.PROJECT_ID, ART.NAME,
				ART.DESCRIPTION, ART.CREATION_DATE, ART.LAST_UPDATE_DATE, PRJ.NAME PROJECT_NAME, CT.RANK
		FROM	TST_REQUIREMENT ART
			INNER JOIN TST_PROJECT PRJ ON ART.PROJECT_ID = PRJ.PROJECT_ID
			INNER JOIN FREETEXTTABLE (TST_REQUIREMENT, *, @SearchString) AS CT
			ON ART.REQUIREMENT_ID = CT.[KEY]  					
		WHERE	PRJ.IS_ACTIVE = 1 AND ART.IS_DELETED = 0
		UNION
		SELECT	2 AS ARTIFACT_TYPE_ID, ART.TEST_CASE_ID AS ARTIFACT_ID, ART.PROJECT_ID, ART.NAME,
				ART.DESCRIPTION, ART.CREATION_DATE, ART.LAST_UPDATE_DATE, PRJ.NAME PROJECT_NAME, CT.RANK
		FROM	TST_TEST_CASE ART
			INNER JOIN TST_PROJECT PRJ ON ART.PROJECT_ID = PRJ.PROJECT_ID
			INNER JOIN FREETEXTTABLE (TST_TEST_CASE, *, @SearchString) AS CT
			ON ART.TEST_CASE_ID = CT.[KEY]  					
		WHERE	PRJ.IS_ACTIVE = 1 AND ART.IS_DELETED = 0
		UNION
		SELECT	3 AS ARTIFACT_TYPE_ID, ART.INCIDENT_ID AS ARTIFACT_ID, ART.PROJECT_ID, ART.NAME,
				ART.DESCRIPTION, ART.CREATION_DATE, ART.LAST_UPDATE_DATE, PRJ.NAME PROJECT_NAME, CT.RANK
		FROM	TST_INCIDENT ART
			INNER JOIN TST_PROJECT PRJ ON ART.PROJECT_ID = PRJ.PROJECT_ID
			INNER JOIN FREETEXTTABLE (TST_INCIDENT, *, @SearchString) AS CT
			ON ART.INCIDENT_ID = CT.[KEY]  					
		WHERE	PRJ.IS_ACTIVE = 1 AND ART.IS_DELETED = 0
		UNION
		SELECT	4 AS ARTIFACT_TYPE_ID, ART.RELEASE_ID AS ARTIFACT_ID, ART.PROJECT_ID, ART.NAME,
				ART.DESCRIPTION, ART.CREATION_DATE, ART.LAST_UPDATE_DATE, PRJ.NAME PROJECT_NAME, CT.RANK
		FROM	TST_RELEASE ART
			INNER JOIN TST_PROJECT PRJ ON ART.PROJECT_ID = PRJ.PROJECT_ID
			INNER JOIN FREETEXTTABLE (TST_RELEASE, *, @SearchString) AS CT
			ON ART.RELEASE_ID = CT.[KEY]  					
		WHERE	PRJ.IS_ACTIVE = 1 AND ART.IS_DELETED = 0
		UNION
		SELECT	5 AS ARTIFACT_TYPE_ID, ART.TEST_RUN_ID AS ARTIFACT_ID, TST.PROJECT_ID, ART.NAME,
				ART.DESCRIPTION, ART.START_DATE AS CREATION_DATE, ART.END_DATE AS LAST_UPDATE_DATE, PRJ.NAME PROJECT_NAME, CT.RANK
		FROM	TST_TEST_RUN ART
			INNER JOIN FREETEXTTABLE (TST_TEST_RUN, *, @SearchString) AS CT ON ART.TEST_RUN_ID = CT.[KEY] 
			INNER JOIN TST_TEST_CASE TST ON ART.TEST_CASE_ID = TST.TEST_CASE_ID
			INNER JOIN TST_PROJECT PRJ ON TST.PROJECT_ID = PRJ.PROJECT_ID				 					
		WHERE	PRJ.IS_ACTIVE = 1 AND TST.IS_DELETED = 0
		UNION
		SELECT	6 AS ARTIFACT_TYPE_ID, ART.TASK_ID AS ARTIFACT_ID, ART.PROJECT_ID, ART.NAME,
				ART.DESCRIPTION, ART.CREATION_DATE, ART.LAST_UPDATE_DATE, PRJ.NAME PROJECT_NAME, CT.RANK
		FROM	TST_TASK ART
			INNER JOIN TST_PROJECT PRJ ON ART.PROJECT_ID = PRJ.PROJECT_ID
			INNER JOIN FREETEXTTABLE (TST_TASK, *, @SearchString) AS CT
			ON ART.TASK_ID = CT.[KEY]  					
		WHERE	PRJ.IS_ACTIVE = 1 AND ART.IS_DELETED = 0
		UNION
		SELECT	7 AS ARTIFACT_TYPE_ID, ART.TEST_STEP_ID AS ARTIFACT_ID, TST.PROJECT_ID, TST.NAME,
				ART.DESCRIPTION, TST.CREATION_DATE, ART.LAST_UPDATE_DATE, PRJ.NAME PROJECT_NAME, CT.RANK
		FROM	TST_TEST_STEP ART
			INNER JOIN FREETEXTTABLE (TST_TEST_STEP, *, @SearchString) AS CT ON ART.TEST_STEP_ID = CT.[KEY] 
			INNER JOIN TST_TEST_CASE TST ON ART.TEST_CASE_ID = TST.TEST_CASE_ID
			INNER JOIN TST_PROJECT PRJ ON TST.PROJECT_ID = PRJ.PROJECT_ID				 					
		WHERE	PRJ.IS_ACTIVE = 1 AND TST.IS_DELETED = 0 AND ART.IS_DELETED = 0
		UNION
		SELECT	8 AS ARTIFACT_TYPE_ID, ART.TEST_SET_ID AS ARTIFACT_ID, ART.PROJECT_ID, ART.NAME,
				ART.DESCRIPTION, ART.CREATION_DATE, ART.LAST_UPDATE_DATE, PRJ.NAME PROJECT_NAME, CT.RANK
		FROM	TST_TEST_SET ART
			INNER JOIN TST_PROJECT PRJ ON ART.PROJECT_ID = PRJ.PROJECT_ID
			INNER JOIN FREETEXTTABLE (TST_TEST_SET, *, @SearchString) AS CT
			ON ART.TEST_SET_ID = CT.[KEY]  					
		WHERE	PRJ.IS_ACTIVE = 1 AND ART.IS_DELETED = 0
		UNION
		SELECT	9 AS ARTIFACT_TYPE_ID, ART.AUTOMATION_HOST_ID AS ARTIFACT_ID, ART.PROJECT_ID, ART.NAME,
				ART.DESCRIPTION, ART.LAST_UPDATE_DATE AS CREATION_DATE, ART.LAST_UPDATE_DATE, PRJ.NAME PROJECT_NAME, CT.RANK
		FROM	TST_AUTOMATION_HOST ART
			INNER JOIN TST_PROJECT PRJ ON ART.PROJECT_ID = PRJ.PROJECT_ID
			INNER JOIN FREETEXTTABLE (TST_AUTOMATION_HOST, *, @SearchString) AS CT
			ON ART.AUTOMATION_HOST_ID = CT.[KEY]  					
		WHERE	PRJ.IS_ACTIVE = 1 AND ART.IS_DELETED = 0
		UNION
		SELECT	13 AS ARTIFACT_TYPE_ID, ART.ATTACHMENT_ID AS ARTIFACT_ID, PAT.PROJECT_ID, ART.FILENAME AS NAME,
				ART.DESCRIPTION, ART.UPLOAD_DATE AS CREATION_DATE, ART.EDITED_DATE AS LAST_UPDATE_DATE, PRJ.NAME PROJECT_NAME, CT.RANK
		FROM	TST_ATTACHMENT ART
			INNER JOIN TST_PROJECT_ATTACHMENT PAT ON ART.ATTACHMENT_ID = PAT.ATTACHMENT_ID
			INNER JOIN TST_PROJECT PRJ ON PAT.PROJECT_ID = PRJ.PROJECT_ID
			INNER JOIN FREETEXTTABLE (TST_ATTACHMENT, *, @SearchString) AS CT
			ON ART.ATTACHMENT_ID = CT.[KEY]  					
		WHERE	PRJ.IS_ACTIVE = 1
		UNION
		SELECT	14 AS ARTIFACT_TYPE_ID, ART.RISK_ID AS ARTIFACT_ID, ART.PROJECT_ID, ART.NAME,
				ART.DESCRIPTION, ART.CREATION_DATE, ART.LAST_UPDATE_DATE, PRJ.NAME PROJECT_NAME, CT.RANK
		FROM	TST_RISK ART
			INNER JOIN TST_PROJECT PRJ ON ART.PROJECT_ID = PRJ.PROJECT_ID
			INNER JOIN FREETEXTTABLE (TST_RISK, *, @SearchString) AS CT
			ON ART.RISK_ID = CT.[KEY]  					
		WHERE	PRJ.IS_ACTIVE = 1 AND ART.IS_DELETED = 0
	)
	AS RES
	WHERE @ProjectArtifactList = '''' OR @ProjectArtifactList IS NULL OR EXISTS
	(
		SELECT 1
		FROM    @ProjectArtifactTable PAL
		WHERE   RES.PROJECT_ID = PAL.PROJECT_ID
		AND		RES.ARTIFACT_TYPE_ID = PAL.ARTIFACT_TYPE_ID
	)
END
');
END
GO
