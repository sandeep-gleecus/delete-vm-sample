-- =====================================================================
-- Author:			Inflectra Corporation
-- Business Object: Attachment
-- Description:		Refreshes the folder hierarchy for the specified project
-- =====================================================================
IF OBJECT_ID ( 'ATTACHMENT_REFRESH_FOLDER_HIERARCHY', 'P' ) IS NOT NULL 
    DROP PROCEDURE ATTACHMENT_REFRESH_FOLDER_HIERARCHY;
GO
CREATE PROCEDURE ATTACHMENT_REFRESH_FOLDER_HIERARCHY
	@ProjectId INT
AS
BEGIN
	--First delete the existing folders
	DELETE FROM TST_PROJECT_ATTACHMENT_FOLDER_HIERARCHY
	WHERE PROJECT_ID = @ProjectId;
	
	WITH PROJECT_ATTACHMENT_FOLDER_HIERARCHY (PROJECT_ATTACHMENT_FOLDER_ID, PROJECT_ID, NAME, PARENT_PROJECT_ATTACHMENT_FOLDER_ID, HIERARCHY_LEVEL, INDENT_LEVEL)
	AS
	(
		SELECT	TKF.PROJECT_ATTACHMENT_FOLDER_ID, TKF.PROJECT_ID, TKF.NAME, TKF.PARENT_PROJECT_ATTACHMENT_FOLDER_ID, 1 AS HIERARCHY_LEVEL,
				CAST(dbo.FN_CREATE_INDENT_LEVEL(ROW_NUMBER() OVER(ORDER BY TKF.NAME)) AS NVARCHAR(MAX)) AS INDENT_LEVEL
		FROM TST_PROJECT_ATTACHMENT_FOLDER TKF
		WHERE TKF.PARENT_PROJECT_ATTACHMENT_FOLDER_ID IS NULL AND TKF.PROJECT_ID = @ProjectId
		UNION ALL
		SELECT	TKF.PROJECT_ATTACHMENT_FOLDER_ID, TKF.PROJECT_ID, TKF.NAME, TKF.PARENT_PROJECT_ATTACHMENT_FOLDER_ID, (CTE.HIERARCHY_LEVEL + 1) AS HIERARCHY_LEVEL,
				CTE.INDENT_LEVEL + dbo.FN_CREATE_INDENT_LEVEL(ROW_NUMBER() OVER(ORDER BY TKF.NAME)) AS INDENT_LEVEL
		FROM TST_PROJECT_ATTACHMENT_FOLDER TKF
		INNER JOIN PROJECT_ATTACHMENT_FOLDER_HIERARCHY CTE ON TKF.PARENT_PROJECT_ATTACHMENT_FOLDER_ID = CTE.PROJECT_ATTACHMENT_FOLDER_ID
		WHERE TKF.PARENT_PROJECT_ATTACHMENT_FOLDER_ID IS NOT NULL AND TKF.PROJECT_ID = @ProjectId
	)
	INSERT INTO TST_PROJECT_ATTACHMENT_FOLDER_HIERARCHY (PROJECT_ATTACHMENT_FOLDER_ID, PROJECT_ID, NAME, PARENT_PROJECT_ATTACHMENT_FOLDER_ID, HIERARCHY_LEVEL, INDENT_LEVEL)
	SELECT ISNULL(PROJECT_ATTACHMENT_FOLDER_ID, 0) AS PROJECT_ATTACHMENT_FOLDER_ID, PROJECT_ID, NAME, PARENT_PROJECT_ATTACHMENT_FOLDER_ID, HIERARCHY_LEVEL, INDENT_LEVEL
	FROM PROJECT_ATTACHMENT_FOLDER_HIERARCHY
	ORDER BY PROJECT_ID, INDENT_LEVEL COLLATE Latin1_General_BIN
END
GO
