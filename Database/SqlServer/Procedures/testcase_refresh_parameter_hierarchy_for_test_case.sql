-- ================================================================
-- Author:			Inflectra Corporation
-- Business Object: TestCase
-- Description:		Refreshes the hierarchy of parameters for a test case
-- ================================================================
IF OBJECT_ID ( 'TESTCASE_REFRESH_PARAMETER_HIERARCHY_FOR_TEST_CASE', 'P' ) IS NOT NULL 
    DROP PROCEDURE TESTCASE_REFRESH_PARAMETER_HIERARCHY_FOR_TEST_CASE;
GO
CREATE PROCEDURE TESTCASE_REFRESH_PARAMETER_HIERARCHY_FOR_TEST_CASE
	@ProjectId INT,
	@TestCaseId INT
AS
BEGIN
	--First drop the existing rows for this project
	DELETE
		FROM TST_TEST_CASE_PARAMETER_HIERARCHY
		WHERE
			PROJECT_ID = @ProjectId
			AND TEST_CASE_ID = @TestCaseId;
	DELETE
		FROM TST_TEST_CASE_PARAMETER_HIERARCHY_ALREADY_SET
		WHERE
			PROJECT_ID = @ProjectId
			AND TEST_CASE_ID = @TestCaseId;

	--First for when we want to include the values set already at lower-levels
	WITH TEST_CASE_PARAMETER_HIERARCHY (TEST_CASE_PARAMETER_ID, TEST_CASE_ID, NAME, DEFAULT_VALUE)
	AS
	(
		SELECT TEST_CASE_PARAMETER_ID, TEST_CASE_ID, NAME, DEFAULT_VALUE
		FROM TST_TEST_CASE_PARAMETER
		UNION ALL
		SELECT TCPH.TEST_CASE_PARAMETER_ID, STP.TEST_CASE_ID AS TEST_CASE_ID, TCPH.NAME, TCPH.DEFAULT_VALUE
		FROM TEST_CASE_PARAMETER_HIERARCHY TCPH INNER JOIN TST_TEST_STEP STP
		ON STP.LINKED_TEST_CASE_ID = TCPH.TEST_CASE_ID INNER JOIN TST_TEST_CASE TST
		ON STP.TEST_CASE_ID = TST.TEST_CASE_ID
		WHERE STP.LINKED_TEST_CASE_ID IS NOT NULL AND PROJECT_ID = @ProjectId
		AND STP.IS_DELETED = 0 AND TST.IS_DELETED = 0
	)
	INSERT INTO TST_TEST_CASE_PARAMETER_HIERARCHY_ALREADY_SET (PROJECT_ID, TEST_CASE_PARAMETER_ID, TEST_CASE_ID, NAME, DEFAULT_VALUE)
	SELECT @ProjectId AS PROJECT_ID, TEST_CASE_PARAMETER_ID, TEST_CASE_ID, NAME, MAX(DEFAULT_VALUE) AS DEFAULT_VALUE
	FROM TEST_CASE_PARAMETER_HIERARCHY
	WHERE TEST_CASE_ID = @TestCaseId
	GROUP BY TEST_CASE_PARAMETER_ID, TEST_CASE_ID, NAME
	ORDER BY TEST_CASE_ID, TEST_CASE_PARAMETER_ID;

	--Next for when we don't want to include the values set already at lower-levels
	WITH TEST_CASE_PARAMETER_HIERARCHY (TEST_CASE_PARAMETER_ID, TEST_CASE_ID, NAME, DEFAULT_VALUE)
	AS
	(
		SELECT TEST_CASE_PARAMETER_ID, TEST_CASE_ID, NAME, DEFAULT_VALUE
		FROM TST_TEST_CASE_PARAMETER
		UNION ALL
		SELECT TCPH.TEST_CASE_PARAMETER_ID, STP.TEST_CASE_ID AS TEST_CASE_ID, TCPH.NAME, TCPH.DEFAULT_VALUE
		FROM TEST_CASE_PARAMETER_HIERARCHY TCPH INNER JOIN TST_TEST_STEP STP
		ON STP.LINKED_TEST_CASE_ID = TCPH.TEST_CASE_ID INNER JOIN TST_TEST_CASE TST
		ON STP.TEST_CASE_ID = TST.TEST_CASE_ID
		WHERE STP.LINKED_TEST_CASE_ID IS NOT NULL AND PROJECT_ID = @ProjectId
		AND NOT TCPH.TEST_CASE_PARAMETER_ID IN
			(SELECT TSP.TEST_CASE_PARAMETER_ID
				FROM TST_TEST_STEP_PARAMETER TSP
				WHERE TSP.TEST_STEP_ID = STP.TEST_STEP_ID)
		AND STP.IS_DELETED = 0 AND TST.IS_DELETED = 0
	)
	INSERT INTO TST_TEST_CASE_PARAMETER_HIERARCHY (PROJECT_ID, TEST_CASE_PARAMETER_ID, TEST_CASE_ID, NAME, DEFAULT_VALUE)
	SELECT @ProjectId AS PROJECT_ID, TEST_CASE_PARAMETER_ID, TEST_CASE_ID, NAME, MAX(DEFAULT_VALUE) AS DEFAULT_VALUE
	FROM	TEST_CASE_PARAMETER_HIERARCHY
	WHERE TEST_CASE_ID = @TestCaseId
	GROUP BY TEST_CASE_PARAMETER_ID, TEST_CASE_ID, NAME
	ORDER BY TEST_CASE_ID, TEST_CASE_PARAMETER_ID;		
END
GO
