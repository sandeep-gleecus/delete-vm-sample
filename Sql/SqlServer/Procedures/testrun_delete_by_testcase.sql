-- =============================================
-- Author:			Inflectra Corporation
-- Business Object: TestRun
-- Description:		Deletes by Test Case ID
-- =============================================
IF OBJECT_ID ( 'TESTRUN_DELETE_BY_TESTCASE', 'P' ) IS NOT NULL 
    DROP PROCEDURE TESTRUN_DELETE_BY_TESTCASE;
GO
CREATE PROCEDURE TESTRUN_DELETE_BY_TESTCASE
	@TestCaseId INT,
	@ArtifactTypeId INT
AS
BEGIN
	SET NOCOUNT ON;
	--Delete the dependent records
 	DELETE FROM TST_TEST_RUN_STEP_INCIDENT
	WHERE TEST_RUN_STEP_ID IN
		(SELECT TRS.TEST_RUN_STEP_ID
		FROM TST_TEST_RUN_STEP TRS INNER JOIN TST_TEST_RUN TRN
		ON TRS.TEST_RUN_ID = TRN.TEST_RUN_ID
		WHERE TRN.TEST_CASE_ID = @TestCaseId);

	DELETE FROM TST_TEST_RUN_STEP WHERE TEST_RUN_ID IN
		(SELECT TEST_RUN_ID FROM TST_TEST_RUN WHERE TEST_CASE_ID = @TestCaseId);
	DELETE FROM TST_ARTIFACT_CUSTOM_PROPERTY
	WHERE ARTIFACT_ID IN
		(SELECT TEST_RUN_ID FROM TST_TEST_RUN WHERE TEST_CASE_ID = @TestCaseId)
	AND ARTIFACT_TYPE_ID = @ArtifactTypeId;

	--Now delete the test run itself
	DELETE FROM TST_TEST_RUN WHERE TEST_CASE_ID = @TestCaseId;
END
GO
